//===== rAthena Script =======================================
//= War of Emperium SE - Template File
//===== By: ==================================================
//= Euphy
//===== Current Version: =====================================
//= 1.4a
//===== Compatible With: =====================================
//= rAthena Project
//===== Description: =========================================
//= Like agit_main, this file is required
//= for SE castles to function.
//===== Additional Comments: =================================
//= 0.x Previous authors: L0ne_W0lf, Zephyrus, Brian.
//= 1.0 If anything breaks, blame Maki. [Euphy]
//= 1.1 Fixed an incorrect label execution. [Euphy]
//= 1.2 Hopefully fixed a processing error. [Euphy]
//= 1.3 Fixed barricade issue in schg_cas02. [Cookie]
//= 1.4 Added OnGuildBreak event and a spawn check. [Euphy]
//= 1.4a Fixed Guardian Stone respawns. [Euphy]
//============================================================

// Core, triggers all other events
//============================================================
-	script	Manager#template	-1,{
OnAgitInit2:
OnRecvCastle2:
	if (strnpcinfo(2) == "template") end;
	if (!getcastledata(strnpcinfo(2),CD_GUILD_ID)) {
		donpcevent strnpcinfo(0)+"::OnStart";
		// Monster spawns are identical for all castles.
		monster strnpcinfo(2),0,0,"闇神官",1117,10;
		monster strnpcinfo(2),0,0,"卡利斯格",1132,4;
		monster strnpcinfo(2),0,0,"深淵騎士",1219,3;
		monster strnpcinfo(2),0,0,"行刑者",1205,1;
		monster strnpcinfo(2),0,0,"梅納海葵",1216,10;
		monster strnpcinfo(2),0,0,"鍾怪",1193,18;
		monster strnpcinfo(2),0,0,"古鐘魔",1269,9;
		monster strnpcinfo(2),0,0,"Raydric Archer",1276,12;
		monster strnpcinfo(2),0,0,"邪骸浪人",1208,3;
		monster strnpcinfo(2),0,0,"愛麗瑟",1275,1;
		monster strnpcinfo(2),0,0,"血腥騎士",1268,2;
		monster strnpcinfo(2),0,0,"黑暗之王",1272,2;
		monster strnpcinfo(2),0,0,"鍾塔守護者",1270,4;
	}
	if (getcastledata(strnpcinfo(2),CD_ENABLED_KAFRA) < 1)
		disablenpc "Kafra Employee#"+substr(strnpcinfo(2),0,1)+substr(strnpcinfo(2),8,9);
	end;

OnAgitStart2:
	if (strnpcinfo(2) == "template") end;
	if (agitcheck2()) {
		maprespawnguildid strnpcinfo(2),getcastledata(strnpcinfo(2),CD_GUILD_ID),2;
		gvgon strnpcinfo(2);
		donpcevent strnpcinfo(0)+"::OnStart";
	}
	else for(set .@i,0; .@i<4; set .@i,.@i+1)
		donpcevent "RL"+.@i+"#"+strnpcinfo(2)+"::OnDisable";
	end;

OnAgitEnd2:
	if (strnpcinfo(2) == "template") end;
	gvgoff strnpcinfo(2);
	if (getcastledata(strnpcinfo(2),CD_GUILD_ID)) {
		set .@str$,substr(strnpcinfo(2),0,1)+substr(strnpcinfo(2),8,9);
		killmonster strnpcinfo(2),"Steward#"+.@str$+"::OnStartArena";
		donpcevent strnpcinfo(0)+"::OnReset";
		donpcevent "Steward#"+.@str$+"::OnStop";
	}
	end;

OnGuildBreak:
	if (strnpcinfo(2) == "template") end;
	killmonster strnpcinfo(2),"gard1#"+strnpcinfo(2)+"::OnGuardianDied";
	killmonster strnpcinfo(2),"gard2#"+strnpcinfo(2)+"::OnGuardianDied";
	disablenpc "Kafra Employee#"+substr(strnpcinfo(2),0,1)+substr(strnpcinfo(2),8,9);
	setcastledata strnpcinfo(2),CD_GUILD_ID,0;
	sleep 7000;
	announce "秘密基地 ["+getcastlename(strnpcinfo(2))+"] 已被遺棄",0;
	donpcevent strnpcinfo(0)+"::OnRecvCastle2";
	end;

OnStart:
	// $agit_ar0x[] - $agit_sc0x[]
	// 第1守護石, 第2守護石, Barrier 1, Barrier 2, Barrier 3, Summon Guardians
	// Settings for all but Summon Guardians: 0 = Okay | 1 = Destroyed | 2 = Repairing
	// Summon Guardians: 0 = Do not Summon | 1 = Summon
	if (getcastledata(strnpcinfo(2),CD_GUILD_ID)) {
		setarray getd("$agit_"+substr(strnpcinfo(2),0,1)+substr(strnpcinfo(2),8,9)+"[0]"),0,0,0,0,0,0;
		donpcevent "df1#"+strnpcinfo(2)+"::OnEnable";
		donpcevent "df2#"+strnpcinfo(2)+"::OnEnable";
		for(set .@i,0; .@i<4; set .@i,.@i+1)
			donpcevent "RL"+.@i+"#"+strnpcinfo(2)+"::OnEnable";
	}

OnEmpSpawn:
	set .@str$, substr(strnpcinfo(2),0,1)+substr(strnpcinfo(2),8,9);
	if (mobcount(strnpcinfo(2),"Steward#"+.@str$+"::OnStartArena")) end;
	if (compare(strnpcinfo(2),"arug")) {
		if (strnpcinfo(2) == "arug_cas01") setarray .@i[0],87,219;
		else if (strnpcinfo(2) == "arug_cas02") setarray .@i[0],89,256;
		else setarray .@i[0],141,293;	// Castles 3,4,5 are identical.
	}
	else {
		if (strnpcinfo(2) == "schg_cas02") setarray .@i[0],162,193;
		else if (strnpcinfo(2) == "schg_cas03") setarray .@i[0],338,202;
		else setarray .@i[0],120,272;	// Castles 1,4,5 are identical.
	}
	monster strnpcinfo(2),.@i[0],.@i[1],"華麗金屬",1288,1,"Steward#"+.@str$+"::OnStartArena";
	end;

OnReset:
	set .@str$, substr(strnpcinfo(2),0,1)+substr(strnpcinfo(2),8,9);
	donpcevent "df1#"+strnpcinfo(2)+"::OnDisable";
	donpcevent "df2#"+strnpcinfo(2)+"::OnDisable";
	donpcevent "gard1#"+strnpcinfo(2)+"::OnReset";
	donpcevent "gard2#"+strnpcinfo(2)+"::OnReset";
	donpcevent "第1守護石#"+.@str$+"::OnDisable";
	donpcevent "第2守護石#"+.@str$+"::OnDisable";
	for(set .@i,1; .@i<4; set .@i,.@i+1)
		donpcevent "控制裝置0"+.@i+"#"+.@str$+"::OnDisable";
	for(set .@i,0; .@i<4; set .@i,.@i+1)
		donpcevent "RL"+.@i+"#"+strnpcinfo(2)+"::OnDisable";
	if (agitcheck2())
		setarray getd("$agit_"+substr(strnpcinfo(2),0,1)+substr(strnpcinfo(2),8,9)+"[0]"),0,0,1,1,1,0;
	end;

OnChange:
	set .@str$, substr(strnpcinfo(2),0,1)+substr(strnpcinfo(2),8,9);
	setarray getd("$agit_"+.@str$+"[0]"),2,2,1,1,2,0;
	donpcevent strnpcinfo(0)+"::OnEmpSpawn";
	donpcevent "控制裝置03#"+.@str$+"::OnEnable";
	donpcevent "第1守護石#"+.@str$+"::OnEnable";
	donpcevent "第2守護石#"+.@str$+"::OnEnable";
	end;

OnClock0001:
	// Spawn Treasure Chests based on castle economy.
	if (strnpcinfo(2) == "template") end;
	if (!getcastledata(strnpcinfo(2),CD_GUILD_ID)) end;
	killmonster strnpcinfo(2),strnpcinfo(0)+"::OnTreasureDied";
	if (getcastledata(strnpcinfo(2),CD_INVESTED_ECONOMY)) {
		set .@Economy,getcastledata(strnpcinfo(2),CD_CURRENT_ECONOMY);
		setcastledata strnpcinfo(2),CD_CURRENT_ECONOMY,.@Economy+getcastledata(strnpcinfo(2),CD_INVESTED_ECONOMY)+(rand(2) && getgdskilllv(getcastledata(strnpcinfo(2),CD_GUILD_ID),10014));
		if (getcastledata(strnpcinfo(2),CD_CURRENT_ECONOMY) > 100) setcastledata strnpcinfo(2),CD_CURRENT_ECONOMY,100;
		setcastledata strnpcinfo(2),CD_INVESTED_ECONOMY,0;
	}
	if (getcastledata(strnpcinfo(2),CD_INVESTED_DEFENSE)) {
		set .@Defence,getcastledata(strnpcinfo(2),CD_CURRENT_DEFENSE);
		setcastledata strnpcinfo(2),CD_CURRENT_DEFENSE,.@Defence+getcastledata(strnpcinfo(2),CD_INVESTED_DEFENSE);
		if (getcastledata(strnpcinfo(2),CD_CURRENT_DEFENSE) > 100) setcastledata strnpcinfo(2),CD_CURRENT_DEFENSE,100;
		setcastledata strnpcinfo(2),CD_INVESTED_DEFENSE,0;
	}
	set .@Treasure,getcastledata(strnpcinfo(2),CD_CURRENT_ECONOMY)/5+4;
	if (!.@Treasure) end;
	freeloop(1);
	if (compare(strnpcinfo(2),"arug")) {
		if (strnpcinfo(2) == "arug_cas01") { 
			set .@treasurebox,1943;
			setarray .@treasurex[0],251,252,253,254,255,256,257,258,251,252,253,254,255,256,257,258,251,252,253,254,255,256,257,258;
			setarray .@treasurey[0],369,369,369,369,368,368,368,368,367,367,367,367,366,366,366,366,365,365,365,365,364,364,364,364;
		}
		else if (strnpcinfo(2) == "arug_cas02") { 
			set .@treasurebox,1944;
			setarray .@treasurex[0],382,383,384,385,386,387,384,385,386,387,388,389,382,383,384,385,386,387,384,385,386,387,388,389;
			setarray .@treasurey[0],231,231,231,231,231,231,230,230,230,230,230,230,225,225,225,225,225,225,224,224,224,224,224,224;
		}
		else { 	// Castles 3,4,5 are identical, except 4's treasure.
			set .@treasurebox,(strnpcinfo(2) == "arug_cas04")?1946:1945;
			setarray .@treasurex[0],291,292,293,294,295,296,293,294,295,296,297,298,291,292,293,294,295,296,293,294,295,296,297,298;
			setarray .@treasurey[0],276,276,276,276,276,276,274,274,274,274,274,274,272,272,272,272,272,272,269,269,269,269,269,269;
		}
	}
	else {
		if (strnpcinfo(2) == "schg_cas02") { 
			set .@treasurebox,1939;
			setarray .@treasurex[0],249,250,251,252,253,246,247,248,249,250,250,251,252,253,246,247,248,249,250,249,250,251,252,253;
			setarray .@treasurey[0],378,378,378,378,378,376,376,376,376,376,374,374,374,374,372,372,372,372,372,370,370,370,370,370;
		}
		else if (strnpcinfo(2) == "schg_cas03") { 
			set .@treasurebox,1940;
			setarray .@treasurex[0],189,190,191,192,193,194,189,190,191,192,193,194,189,190,191,192,193,194,189,190,191,192,193,194;
			setarray .@treasurey[0], 21, 21, 21, 21, 21, 21, 19, 19, 19, 19, 19, 19, 17, 17, 17, 17, 17, 17, 15, 15, 15, 15, 15, 15;
		}
		else {	// Castles 1,4,5 are identical, except treasures.
			if (strnpcinfo(2) == "schg_cas01") set .@treasurebox,1938;
			else if (strnpcinfo(2) == "schg_cas04") set .@treasurebox,1941;
			else set .@treasurebox,1942;
			setarray .@treasurex[0],388,388,388,387,386,385,384,384,384,384,384,384,385,386,387,388,389,390,390,390,389,388,387,386;
			setarray .@treasurey[0],388,389,390,390,390,390,389,388,387,386,385,384,384,384,384,384,384,384,385,386,386,386,386,386;
		}
	}
	for(set .@i,0; .@i<4; set .@i,.@i+1)
		monster strnpcinfo(2),.@treasurex[.@i],.@treasurey[.@i],"Treasure Chest",(.@i%2)?.@treasurebox:1324,1,strnpcinfo(0)+"::OnTreasureDied";
	for(set .@i,4; .@i<24; set .@i,.@i+1) {
		if (.@Treasure < .@i+1) break;
		monster strnpcinfo(2),.@treasurex[.@i],.@treasurey[.@i],"Treasure Chest",(.@i%2)?.@treasurebox:1324,1,strnpcinfo(0)+"::OnTreasureDied";
	}
	freeloop(0);
	end;

OnTreasureDied:
	end;
}

// Guild Manager
//============================================================
-	script	Steward#template	-1,{
	set .@GID, getcastledata(strnpcinfo(4),CD_GUILD_ID);
	if (!.@GID) {
		mes "[ 管理人 ]";
		mes "我在此等待我的主人... 勇敢的冒險者啊! 追隨你的命運吧!";
		close;
	}
	if (is_guild_leader(.@GID) == false) {
		mes "[ 管理人 ]";
		mes "不管你們怎麼威脅我";
		mes "我只願意追隨我的主人 ^FF0000"+getguildmaster(.@GID)+"^000000.";
		close;
	}
	mes "[ 管理人 ]";
	mes "歡迎光臨,我的主人 ^FF0000"+getguildmaster(.@GID)+"^000000...";
	mes "請儘管吩咐,我將忠誠地為你完成任務";
	next;
	switch(select("查看秘密基地狀況:投資商業發展度:投資防禦度:雇用 / 解雇 倉庫職員:進入主人房間")) {
	case 1:
		mes "[ 管理人 ]";
		mes "現在向你報告秘密基地的狀況";
		mes "現在商業發展度是 ^0000ff"+getcastledata(strnpcinfo(4),CD_CURRENT_ECONOMY)+".";
		if (getcastledata(strnpcinfo(4),CD_INVESTED_ECONOMY) > 0) {
			mes "在過去一天之內";
			mes "投資了 "+getcastledata(strnpcinfo(4),CD_INVESTED_ECONOMY)+" 次";
		}
		next;
		mes "[ 管理人 ]";
		mes "現在的防禦度是 "+getcastledata(strnpcinfo(4),CD_CURRENT_DEFENSE)+".";
		if (getcastledata(strnpcinfo(4),CD_INVESTED_DEFENSE) > 0) {
			mes "在過去一天之內";
			mes "投資了 "+getcastledata(strnpcinfo(4),CD_INVESTED_DEFENSE)+" 次";
		}
		mes " ";
		mes "報告完畢";
		close;
	case 2:
		set .@Economy,getcastledata(strnpcinfo(4),CD_CURRENT_ECONOMY);
		setarray .@cost[0],5000,10000,20000,35000,55000,80000,110000,145000,185000,230000,280000,335000,395000,460000,530000,605000,685000,770000,860000,955000;
		set .@j,0;
		for(set .@i,6; .@i<101; set .@i,.@i+5) {
			if (.@Economy < .@i) {
				set .@eco_invest,.@cost[.@j];
				break;
			}
			set .@j, .@j+1;
		}
		// Quadruple the cost of investing if you've already invested once.
		if (getcastledata(strnpcinfo(4),CD_INVESTED_ECONOMY)) set .@eco_invest,.@eco_invest*4;
		mes "[ 管理人 ]";
		mes "提高商業發展度的話,在工會裡生產的物品數量會增加,為了以後著想的話,不能不投資啊";
		mes " ";
		next;
		mes "照原先的方式,一天只能投資一次,假如多付一點金錢的話,一天可以投資兩次";
		mes " ";
		next;
		if (.@Economy == 100) {
			mes "[ 管理人 ]";
			mes "^FF0000目前防禦度以經是100了,你已經不需要再加入任何投資了^000000";
			close;
		}
		if (getcastledata(strnpcinfo(4),CD_INVESTED_ECONOMY) >= 2) {
			mes "[ 管理人 ]";
			mes "^FF0000可是你已經在今天之內投資了兩次再不能投資了,^000000讓發展度趕快上升期待著富貴的日子囉";
			close;
		}
		if (getcastledata(strnpcinfo(4),CD_INVESTED_ECONOMY) == 0) {
			mes "[ 管理人 ]";
			mes "現在需要投資的金額是 ^FF0000"+.@eco_invest+"^000000 Zeny";
			mes "請問你要投資嗎?";
		}
		else {
			mes "[ 管理人 ]";
			mes "還要再投資嗎? 還需要追加^FF0000"+.@eco_invest+"^000000 Zeny";
		}
		next;
		switch(select("投資商業發展度:取消")) {
		case 1:
			if (getcastledata(strnpcinfo(4),CD_INVESTED_ECONOMY) >= 2) {
				mes "[ 管理人 ]";
				mes "You've already made two";
				mes "investments today, so you'll";
				mes "have to wait until tomorrow";
				mes "to make another investment.";
				close;
			}
			if (Zeny < .@eco_invest) {
				mes "[ 管理人 ]";
				mes "主人,你沒有足夠的資金可以投資商業發展度";
				close;
			}
			set Zeny, Zeny-.@eco_invest;
			setcastledata strnpcinfo(4),CD_INVESTED_ECONOMY,getcastledata(strnpcinfo(4),CD_INVESTED_ECONOMY)+1;
			mes "[ 管理人 ]";
			mes "順利地完成投資了!";
			mes "期待著我們往後要面臨的未來,明天的話發展度會上升";
			close;
		case 2:
			mes "[ 管理人 ]";
			mes "我將依照你的吩咐去執行,是我的主人";
			close;
		}
	case 3:
		set .@Defence,getcastledata(strnpcinfo(4),CD_CURRENT_DEFENSE);
		setarray .@cost[0],10000,20000,40000,70000,110000,160000,220000,290000,370000,460000,560000,670000,790000,920000,1060000,1210000,1370000,1540000,1720000,1910000;
		set .@j,0;
		for(set .@i,6; .@i<101; set .@i,.@i+5) {
			if (.@Defence < .@i) {
				set .@def_invest,.@cost[.@j];
				break;
			}
			set .@j, .@j+1;
		}
		// Quadruple the cost of investing if you've already invested once.
		if (getcastledata(strnpcinfo(4),CD_INVESTED_DEFENSE)) set .@def_invest,.@def_invest*4;
		mes "[ 管理人 ]";
		mes "提高防禦程度的話,監護人和華麗金屬的耐久度會增加,考慮到往後的戰爭,不得不投資啊,";
		mes " ";
		next;
		mes "照原先的方式,一天只能投資一次,假如多付一點金錢的話,一天可以投資兩次";
		mes " ";
		next;
		mes "[ 管理人 ]";
		if (getcastledata(strnpcinfo(4),CD_CURRENT_DEFENSE) == 100) {
			mes "^FF0000目前防禦度以經是100了,你已經不需要再加入任何投資了^000000";
			close;
		}
		if (getcastledata(strnpcinfo(4),CD_INVESTED_DEFENSE) >= 2) {
			mes "^FF0000可是你已經在今天之內投資了兩次再不能投資了,^000000讓發展度趕快上升期待著富貴的日子囉";
			close;
		}
		if (getcastledata(strnpcinfo(4),CD_INVESTED_DEFENSE) == 0) {
			mes "現在需要投資的金額是 ^FF0000"+.@def_invest+"^000000";
			mes "請問你要投資嗎?";
		}
		else {
			mes "還要再投資嗎? 還需要追加^FF0000"+.@def_invest+"^000000";
		}
		next;
		switch(select("投資防禦度:取消")) {
		case 1:
			if (getcastledata(strnpcinfo(4),CD_INVESTED_DEFENSE) >= 2) {
			mes "[ 管理人 ]";
			mes "^FF0000可是你已經在今天之內投資了兩次再不能投資了,^000000讓發展度趕快上升期待著富貴的日子囉";
			close;
			}
			if (Zeny < .@def_invest) {
			mes "[ 管理人 ]";
			mes "主人,你沒有足夠的資金可以投資防禦度";
			close;
			}
			set Zeny, Zeny-.@def_invest;
			setcastledata strnpcinfo(4),CD_INVESTED_DEFENSE,getcastledata(strnpcinfo(4),CD_INVESTED_DEFENSE)+1;
			mes "[ 管理人 ]";
			mes "順利地完成投資了!";
			mes "期待著我們往後要面臨的未來,明天的話發展度會上升";
			close;
		case 2:
			mes "[ 管理人 ]";
			mes "我將依照你的吩咐去執行,是我的主";
			close;
		}
	case 4:
		if (getcastledata(strnpcinfo(4),CD_ENABLED_KAFRA) == 1) {
			mes "[ 管理人 ]";
			mes "請問您要解雇卡普拉服務人員嗎?";
			next;
			switch(select("解雇:取消")) {
			case 1:
				cutin "kafra_01",2;
				mes "[ 卡普拉 職員 ]";
				mes "我做錯了什麼事嗎?如果我有做錯事的話,能請您原諒我,再給我一次機會嗎?";
				next;
				switch(select("解雇:取消")) {
				case 1:
					mes "[ 卡普拉 職員 ]";
					mes "不能夠繼續為您服務真是我最大的遺憾...";
					next;
					cutin "kafra_01",255;
					break;
				case 2:
					mes "[ 卡普拉 職員 ]";
					mes "我會努力勤奮的為您服務的!";
					close;
				}
				break;
			case 2:
				mes "[ 管理人 ]";
				mes "主人,我想我們應該要留下現在這位卡普拉職員,因為她已經竭盡所能地為我們服務了!";
				close;
			}
			disablenpc "Kafra Employee#"+strnpcinfo(2);
			setcastledata strnpcinfo(4),CD_ENABLED_KAFRA,0;
			mes "[ 管理人 ]";
			mes "卡普拉職員已經解雇了... 不過為了辛苦的會員著想,雇用卡普拉職員為上策啊";
			close;
		}
		else {
			mes "[ 管理人 ]";
			mes "和卡普拉總公司訂契約後,想要在秘密基地裡面,雇用卡普拉職員嗎?";
			mes "想要雇用的話,需要 ^FF000010,000 金幣^000000";
			next;
			switch(select("雇用:取消")) {
			case 1:
				if (getgdskilllv(.@GID,10001) == 0) {
					mes "[ 管理人 ]";
					mes "主人,如今還沒跟卡普拉總公司訂契約狀況,所以還不能雇用卡普拉職員,假如想要跟卡普拉總公司簽訂契約的話,先學會工會技能中 ^FF0000和卡普拉訂契約^000000的技能吧";
					close;
				}
				if (Zeny < 10000) {
					mes "[ 管理人 ]";
					mes "主人,你沒有足夠的資金可以雇用卡普拉職員";
					close;
				}
				set Zeny, Zeny-10000;
				enablenpc "Kafra Employee#"+strnpcinfo(2);
				setcastledata strnpcinfo(4),CD_ENABLED_KAFRA,1;
				mes "[ 管理人 ]";
				mes "成功的和卡普拉總公司簽訂契約了,您雇用了一位卡普拉職員!";
				next;
				cutin "kafra_01",2;
				mes "[ 卡普拉 職員 ]";
				mes "您好嗎?我是總公司派來的卡普拉職員!我將會竭盡所能地為您服務!";
				next;
				cutin "kafra_01",255;
				mes "[ 管理人 ]";
				mes "雇用的卡普拉職員將有^FF00001 個月的時間^000000在此服務,時間到了之後必須重新簽訂契約";
				close;
			case 2:
				mes "[ 管理人 ]";
				mes "我已經按照你的吩咐做了,不過也要為了辛苦的會員著想,早日雇用卡普拉職員為上策啊";
				close;
			}
		}
	case 5:
		mes "[ 管理人 ]";
		mes "要到我們生產品囤積的,真貴物品房間嗎?";
		mes "那個地方是為了我們的主人,的專屬於你的地方...";
		mes "除了你之外,其它人都不能其門而入";
		next;
		mes "[ 管理人 ]";
		mes "會有物品消失的情形,敬請特別注意,";
		mes "所以為了我們公會發展,請挪出時間來收取物品";
		next;
		switch(select("要進入主人的房間:取消")) {
		case 1:
			mes "[ 管理人 ]";
			mes "我將為你服務到祕密通道那裡,請跟我來,";
			mes "回來的時候按下秘密按鈕即可";
			close2;
			if (compare(strnpcinfo(4),"arug")) {
				if (strnpcinfo(4) == "arug_cas01") setarray .@i[0],250,363;
				else if (strnpcinfo(4) == "arug_cas02") setarray .@i[0],382,227;
				else setarray .@i[0],292,266;	// Castles 3,4,5 are identical.
			}
			else {
				if (strnpcinfo(4) == "schg_cas02") setarray .@i[0],249,373;
				else if (strnpcinfo(4) == "schg_cas03") setarray .@i[0],190,16;
				else setarray .@i[0],381,381;	// Castles 1,4,5 are identical.
			}
			warp strnpcinfo(4),.@i[0],.@i[1];
			end;
		case 2:
			mes "[ 管理人 ]";
			mes "生產品一天只生產一次...";
			mes "假如沒有即時收走的話,下一次就不能進來了";
			mes "所以說為了我們工會的發展,盡量花點時間去收取啊";
			close;
		}
	}

OnStop:
	awake strnpcinfo(0);
	end;

OnStartArena:
	set .@GID,getcharid(2);
	set .@region$, (compare(strnpcinfo(4),"arug"))?"發菲依雅":"尼它堆";
	// Lower castle Economy
	set .@Economy,getcastledata(strnpcinfo(4),CD_CURRENT_ECONOMY)-5;
	if (.@Economy < 0) set .@Economy, 0;
	setcastledata strnpcinfo(4),CD_CURRENT_ECONOMY,.@Economy;
	// Lower Castle Defence
	set .@Defence,getcastledata(strnpcinfo(4),CD_CURRENT_DEFENSE)-5;
	if (.@Defence < 0) set .@Defence, 0;
	setcastledata strnpcinfo(4),CD_CURRENT_DEFENSE,.@Defence;
	// Set new owner
	setcastledata strnpcinfo(4),CD_GUILD_ID,.@GID;
	// Clear castle's data.
	for(set .@i,CD_INVESTED_ECONOMY; .@i<CD_ENABLED_GUARDIAN00; set .@i,.@i+1)
		setcastledata strnpcinfo(4),.@i,0;
	// Disable Kafra
	disablenpc "Kafra Employee#"+strnpcinfo(2);

	announce "["+getguildname(.@GID)+"] 公會佔領了 ["+.@region$+" "+charat(strnpcinfo(2),3)+"] 地區的秘密基地 "+getcastlename(strnpcinfo(4))+"!",bc_all|bc_woe;
	mapannounce strnpcinfo(4),"華麗金屬已被破壞掉了!",bc_map,"0x00FF00",FW_NORMAL,20,0,40;
	donpcevent "Manager#"+strnpcinfo(4)+"::OnReset";
	maprespawnguildid strnpcinfo(4),getcastledata(strnpcinfo(4),CD_GUILD_ID),2;
	donpcevent "Manager#"+strnpcinfo(4)+"::OnRecvCastle2";
	donpcevent "::OnRecvCastle"+strnpcinfo(2);
	sleep 10000;
	if (agitcheck2()) {
		donpcevent "Manager#"+strnpcinfo(4)+"::OnChange";
		mapannounce strnpcinfo(4),"在各個據點和防禦線的修復點數 已登場了!",bc_map,"0x00FF00",FW_NORMAL,20,0,40;
	}
	end;
}

// Castle Guardians
//============================================================
-	script	Guardian#template	-1,{
	set .@GID, getcastledata(strnpcinfo(4),CD_GUILD_ID);
	set .@n$, "["+strnpcinfo(1)+"]";
	if (!.@GID) {
		mes .@n$;
		mes "何許人物?";
		mes "請你快一點離開這座城池吧!";
		close;
	}
	if (getcharid(2) == .@GID) {
		if (is_guild_leader() == false) {
			mes .@n$;
			mes "我是負責城池的守護,";
			mes "詳細的內容我想跟主人說";
			close;
		}
		else {
			if (!agitcheck2()) {
				mes .@n$;
				mes "我是這座城的監護人";
				mes "這個據點,現在";
				mes "一切都很平靜";
				next;
				switch(select("交談:取消")) {
				case 1:
					mes .@n$;
					mes "你有什麼問題";
					mes "有關於這據點的問題?";
					next;
					switch(select("守護石:防禦線:空間移動系統:戰略資訊:取消")) {
					case 1:
						mes .@n$;
						mes "每一座城堡";
						mes "都會有二個守護石";
						mes "每個城堡的守護石";
						mes "是第一道防線,";
						mes "在他尚未銷毀之前";
						mes "敵人是無法進入到城堡內部的.";
						next;
						mes "[伊僕]";
						mes "守護石位於";
						mes "^4D4DFF據點中^000000";
						mes "你需要防守這邊,";
						mes "以防止敵人破壞他.";
						mes "守護石^4D4DFF可以經由傳送到達";
						mes "你的守護者^000000會保護他.";
						next;
						mes "[伊僕]";
						mes "城堡具有較高防禦度";
						mes "可以召喚更多的防守護人";
						mes "這就是為什麼它是";
						mes "此重要的投資";
						mes "防禦度可以經由投資增加.";
						next;
						mes "[伊僕]";
						mes "守護石";
						mes "可以恢復以及被破壞";
						mes "經過一定時間以後,";
						mes "工會成員可以修復.";
						close;
					case 2:
						mes .@n$;
						mes "^4D4DFF防禦線^000000^FFFFFF是第二個 ^000000公會需要防禦的據點,";
						mes "玩家可以在防禦線後方狙擊敵人.";
						mes "這些防禦線設在三個不同地區的入口.";
						next;
						mes "[伊僕]";
						mes "防禦線受到守護石的保護";
						mes "當守護石恢復時將會是監護人重生";
						mes "但是守護石不是那麼容易恢復";
						mes "必須由公會成員使用適當材料來修復,";
						close;
					case 3:
						mes .@n$;
						mes "各個據點";
						mes "都可以經由旗幟自由的傳送";
						mes "進入防守的區域中";
						mes "限制條件:";
						mes "每次傳送都有^4D4DFFF延遲時間";
						mes "等延遲時間過後才可以在傳送到其他據點^000000.";
						close;
					case 4:
						mes .@n$;
						mes "戰略? 不如";
						mes "發展你的作戰計劃";
						mes "利用你的公會的優勢";
						mes "和你的敵人對決.";
						mes "使用防護線和路障有效的狙擊敵人!";
						close;
					case 5:
						mes .@n$;
						mes "我一直在這裡，所以";
						mes "當你需要幫忙時";
						mes "隨時可以來找我";
						close;
					}
				case 2:
					mes .@n$;
					mes "I'm always here, so";
					mes "feel free to request my";
					mes "assistance whenever";
					mes "the need arises.";
					close;
				}
			}
			else {
				mes .@n$;
				mes "您好, "+strcharinfo(0)+".";
				mes "你想選擇什麼?";
				next;
				switch(select("增加城堡防禦:檢查防禦狀態:取消")) {
				case 1:
					if (!getd("$agit_"+strnpcinfo(2)+"[5]")) {
						if (getgdskilllv(.@GID,10002) == 0) {
							mes .@n$;
							mes "我很抱歉";
							mes "守護石目前還不夠強大";
							mes "無法在召喚守護者呢";
							mes "需要積累更多";
							mes "防禦度才能";
							mes "招喚更多的守護者.";
							close;
						}
						else {
							mes .@n$;
							mes "我將努力召集";
							mes "更多的守護者";
							mes "不過，請記住";
							mes "如果守護石被破壞";
							mes "將會使得守護者減少.";
							setd "$agit_"+strnpcinfo(2)+"[5]",1;
							if (!getd("$agit_"+strnpcinfo(2)+"[0]"))
								donpcevent "gard1#"+strnpcinfo(4)+"::OnEnable";
							if (!getd("$agit_"+strnpcinfo(2)+"[1]"))
								donpcevent "gard2#"+strnpcinfo(4)+"::OnEnable";
							close;
						}
					}
					else {
						mes .@n$;
						mes "您已經吩咐";
						mes "我召喚守護者";
						mes "保衛堡壘.";
						close;
					}
				case 2:
					mes .@n$;
					mes "Our defense status is...";
					setarray .@status$[0],"^4D4DFFOperational","^FF0000Destroyed","^008000Repairing";
					mes "第1守護石: "+.@status$[getd("$agit_"+strnpcinfo(2)+"[0]")]+"^000000";
					mes "第2守護石: "+.@status$[getd("$agit_"+strnpcinfo(2)+"[1]")]+"^000000";
					mes "第1防禦線: "+.@status$[getd("$agit_"+strnpcinfo(2)+"[2]")]+"^000000";
					mes "第2防禦線: "+.@status$[getd("$agit_"+strnpcinfo(2)+"[3]")]+"^000000";
					mes "第3防禦線: "+.@status$[getd("$agit_"+strnpcinfo(2)+"[4]")]+"^000000";
					close;
				case 3:
					mes .@n$;
					mes "我隨時在此,";
					mes "等候您的指示";
					close;
				}
			}
		}
	}
	else {
		mes .@n$;
		mes "何許人物?";
		mes "請你快一點離開這座城池吧!";
		close;
	}

OnInit:
	setarray getd("$agit_"+strnpcinfo(2)+"[0]"),0,0,0,0,0,0;
	end;
}

// Guild Kafras
//============================================================
-	script	Kafra#template	-1,{
	cutin "kafra_01",2;
	set .@GID, getcastledata(strnpcinfo(4),CD_GUILD_ID);
	if (getcharid(2) == .@GID && getgdskilllv(.@GID,10001)) {
		mes "[卡普拉 職員]";
		mes "歡迎光臨 ^FF0000"+GetGuildName(.@GID)+"^000000 Guild!";
		mes "卡普拉服務";
		mes "永遠在各位身邊為您服務的";
		next;
		switch(select("使用倉庫服務:使用空間移動服務:使用手推車服務:結束")) {
		case 1:
			if(!callfunc("F_CanOpenStorage")){
				mes "[卡普拉 職員]";
				mes "對不起";
				mes "想要使用倉庫服務的話,初學者的基本技能等級要到達6級以上";
			}
			else openstorage;
			break;
		case 2:
			mes "[卡普拉 職員]";
			mes "請說出目的地吧";
			next;
			switch(select("朱諾 -> 200 z:取消")) {
			case 1:
				if (Zeny < 200) {
					mes "[卡普拉 職員]";
					mes "您的金額不足,";
					mes "請再確認";
					close2;
					cutin "kafra_01",255;
					end;
				}
				set Zeny, Zeny-200;
				warp "rachel",115,125;
				end;
			case 2:
				cutin "kafra_01",255;
				break;
			}
			break;
		case 3:
			if (BaseClass != Job_Merchant) {
				mes "[卡普拉 職員]";
				mes "很抱歉,";
				mes "手推車是商人和鐵匠,鍊金術師專用的服務";
			}
			else if (checkcart() == 1) {
				mes "[卡普拉 職員]";
				mes "您已經租過手推車了";
			}
			else {
				mes "[卡普拉 職員]";
				mes "手推車使用費用是 800 金幣";
				mes "您要使用嗎?";
				next;
				switch(select("使用:取消")) {
				case 1:
					if (Zeny < 800) {
						mes "[卡普拉 職員]";
						mes "您的金額不足,";
						mes "手推車使用費用是 800 金幣";
						close2;
						cutin "kafra_01",255;
						end;
					}
					set Zeny, Zeny-800;
					setcart;
					break;
				case 2:
					break;
				}
			}
			break;
		case 4:
			mes "[卡普拉 職員]";
			mes "我們卡普拉服務會提供更完美的服務和嚴格的業務管理,讓各位顧客安心便利的使用為最大的目標";
			mes "感謝你使用卡普拉服務";
			close2;
			cutin "kafra_01",255;
			end;
		}
		close2;
		cutin "kafra_01",255;
		end;
	}
	else {
		mes "[卡普拉 職員]";
		mes "我已簽訂契約專為";
		mes "^FF0000"+getguildname(.@GID)+"^000000 Guild.";
		mes "請使用我們其它地方的卡普拉職員,";
		mes "如有不便之處敬請見諒";
		close2;
		cutin "kafra_01",255;
		end;
	}
}

// Guardian Stones (2)
//============================================================
-	script	Guardian Stone#template	-1,{
	set .@GID, getcastledata(strnpcinfo(4),CD_GUILD_ID);
	set .@num, atoi(charat(strnpcinfo(1),0));
	set .@var$,"$agit_"+strnpcinfo(2);
	if (getcharid(2) == .@GID) {
		mes "把破壞掉的守護石要重新形成了,";
		mes "請問要繼續的進行作業嗎?";
		next;
		mes "所需材料:";
		mes "1 鋁";
		mes "1 神之金屬";
		mes "30 石頭";
		mes "5 藍色魔力礦石";
		mes "5 黃色魔力礦石";
		mes "5 紅色魔力礦石";
		next;
		mes "^3355FF你想繼續嗎?^000000";
		next;
		if(select("結束:進行") == 1) {
			mes "^3355FF工作取消.^000000";
			close;
		}
		if ((countitem(984) > 0) && (countitem(985) > 0) && (countitem(7049) > 29) && (countitem(717) > 4) && (countitem(715) > 4) && (countitem(716) > 4)) {
				mes "中央要依據";
				mes "石頭,鋁,神之金屬的順序來配置,";
				mes "為了要重新形成";
				mes "要配置有魔力的魔力礦石";
			next;
			setarray .@stone$[0],"鋁","神之金屬","石頭";
			set .@i, select("鋁:神之金屬:石頭")-1;
			if (.@i == 2) set .@nice,.@nice+10;
			mes ""+.@stone$[.@i]+" ";
			mes "正確放上增加防禦10";
			next;
			set .@i, select("鋁:神之金屬:石頭")-1;
			if (.@i == 0) set .@nice,.@nice+10;
			mes ""+.@stone$[.@i]+" ";
			mes "正確放上增加防禦10";
			next;
			set .@i, select("鋁:神之金屬:石頭")-1;
			if (.@i == 1) set .@nice,.@nice+10;
			mes ""+.@stone$[.@i]+" ";
			mes "正確放上增加防禦10";
			next;
				mes "不違背魔法式";
				mes "要配置有魔力的魔力礦石,";
				mes "魔力的性質用準備的效果以便可得知";
			next;
			setarray .@effect[0],EF_BEGINSPELL4,EF_BEGINSPELL2,EF_VOLCANO;
			setarray .@color$[0],"紅色","黃色","藍色";
			while(1) {
				if (.@roof0 > 7) break;
				set .@i, rand(getarraysize(.@effect));
				specialeffect .@effect[.@i];
				mes "請依照散發的顏色配置對應的魔力礦石";
				next;
				set .@j, select("紅色魔力礦石:黃色魔力礦石:藍色魔力礦石")-1;
				mes "^3355FF您放置了 "+.@color$[.@j]+"魔力礦石^000000";
				if (.@i == .@j) {
					mes "因魔力互相的衝突";
					mes "對守護石重新的形成失敗了";
					close;
				}
				set .@nice,.@nice+10;
				set .@roof0,.@roof0+1;
				specialeffect EF_STEAL;
				next;
			}
			if (.@nice > 90) {
				if (!getd(.@var$+"["+(.@num-1)+"]")) {
					mes "守護石重新的形成已經被完成了";
					close;
				}
				else {
					if (!agitcheck2()) {
						mes "^3355FFIt is impossible to";
						mes "rebuild the Guardian";
						mes "Stone because the";
						mes "Emperium is not present.^000000";
						close;
					}
					else {
						mes "已完成了魔力礦石的配置,";
						mes "守護石的重新形成是成功的!!!";
						delitem 984,1; //Oridecon
						delitem 985,1; //Elunium
						delitem 7049,30; //Stone
						delitem 717,5; //Blue_Gemstone
						delitem 715,5; //Yellow_Gemstone
						delitem 716,5; //Red_Gemstone
						close2;
						donpcevent "df"+.@num+"#"+strnpcinfo(4)+"::OnEnable";
						specialeffect EF_ICECRASH;
						disablenpc();
						setd .@var$+"["+(.@num-1)+"]",0;
						set .@df_all,getd(.@var$+"[0]")+getd(.@var$+"[1]");
						if (!.@df_all) {
							mapannounce strnpcinfo(4),"守護石已完全的修復 開始強化警戒了!",bc_map,"0x00ff00";
							donpcevent "RL0#"+strnpcinfo(4)+"::OnEnable";
						}
						else mapannounce strnpcinfo(4),"第 "+strnpcinfo(1)+" 守護石重新形成 已成功了!",bc_map,"0x00ff00";
						if (getd(.@var$+"[5]") == 1)
							donpcevent "gard"+.@num+"#"+strnpcinfo(4)+"::OnEnable";
						end;
					}
				}
			}
			else {
				mes "所有的作業都做完後.. 碰咚碰咚的響著";
				mes "沒有任何的反應";
				mes "看樣子是失敗的樣子,";
				delitem 7049,10; //Stone
				delitem 717,2; //Blue_Gemstone
				delitem 715,2; //Yellow_Gemstone
				delitem 716,2; //Red_Gemstone
				close;
			}
		}
		else {
			mes "所需材料不足";
			close;
		}
	}
	end;

OnInit:
OnDisable:
	disablenpc();
	end;

OnEnable:
	enablenpc();
	specialeffect EF_MAPPILLAR2;
	end;
}

// 控制裝置s (3)
//============================================================
-	script	Control#template	-1,{
	set .@GID, getcastledata(strnpcinfo(4),CD_GUILD_ID);
	set .@num, atoi(charat(strnpcinfo(1),15));
	set .@var$,"$agit_"+strnpcinfo(2);
	if (getcharid(2) == .@GID) {
		if (is_guild_leader() == true) {
			if (getd(.@var$+"["+(.@num+1)+"]") == 2) {
				mes "^3355FF你占領據點後";
				mes "防禦線是可以修復的,";
				next;
				mes "修復所需材料:";
				mes "^4D4DFF10 鋼鐵^000000";
				mes "^4D4DFF30 瑪哪樹枝^000000";
				mes "^4D4DFF5 神之金屬^000000";
				mes "^4D4DFF10 強化武器金屬-級數二^000000";
				next;
				select("Continue");
				if ((countitem(1019) > 29) && (countitem(999) > 9) && (countitem(1011) > 9) && (countitem(984) > 4)) {
					mes "準備好材料就可以開始進行修復";
					next;
					set .@ro_of01,rand(10,15);
					while(1) {
						if (.@ro_of02 == .@ro_of01) break;
						else {
							switch(rand(1,4)) {
							case 1:
								mes "^3355FF支撐者防禦線的框架,";
								mes "已經嚴重損壞,";
								mes "必須馬上將他固定";
								mes "所需材料:瑪哪樹枝.^000000";
								next;
								switch(select("瑪哪樹枝:鋼鐵:強化武器金屬-級數二:神之金屬")) {
								case 1:
									mes "^3355FF防禦線的框架";
									mes "經由木材的強化已經修復了.^000000";
									set .@rp_temp,.@rp_temp+1;
									set .@ro_of02,.@ro_of02+1;
									specialeffect2 EF_REPAIRWEAPON;
									next;
									break;
								case 2:
									mes "^3355FF您試圖使用鋼鐵修復,";
									mes "但他並非現在所需要的材料";
									mes "你必須在嘗試使用";
									mes "其他的材料來修復防禦線.^000000";
									close;
								case 3:
									mes "^3355FF您試圖使用強化武器金屬修復,";
									mes "但他並非現在所需要的材料";
									mes "你必須在嘗試使用";
									mes "其他的材料來修復防禦線.^000000";
									close;
								case 4:
									mes "^3355FF您試圖使用神之金屬修復,";
									mes "但他並非現在所需要的材料";
									mes "你必須在嘗試使用";
									mes "其他的材料來修復防禦線.^000000";
									close;
								}
								break;
							case 2:
								mes "^3355FF它看起來像防禦線的";
								mes "防禦力已經嚴重降低";
								mes "需要使用加強防禦的材料";
								mes "所需材料:神之金屬.^000000";
								next;
								switch(select("瑪哪樹枝:鋼鐵:強化武器金屬-級數二:神之金屬")) {
								case 1:
									mes "^3355FF您試圖使用木材";
									mes "加強防禦線.^000000";
									set .@ro_of02,.@ro_of02+1;
									next;
									break;
								case 2:
									mes "^3355FF您試圖使用鋼鐵修復,";
									mes "但他並非現在所需要的材料";
									mes "你必須在嘗試使用";
									mes "其他的材料來修復防禦線.^000000";
									close;
								case 3:
									mes "^3355FF您試圖使用強化武器金屬修復,";
									mes "但他並非現在所需要的材料";
									mes "你必須在嘗試使用";
									mes "其他的材料來修復防禦線.^000000";
									close;
								case 4:
									mes "^3355FF你重建了防禦線";
									mes "它看起來";
									mes "就跟全新的一樣.^000000";
									set .@rp_temp,.@rp_temp+1;
									set .@ro_of02,.@ro_of02+1;
									specialeffect2 EF_REPAIRWEAPON;
									next;
									break;
								}
								break;
							case 3:
								mes "^3355FF這種損害的防禦線";
								mes "已經布滿了裂痕";
								mes "你必須準備焊接的材料";
								mes "將這些裂痕焊接牢固";
								mes "所需材料:鋼鐵.^000000";
								next;
								switch(select("瑪哪樹枝:鋼鐵:強化武器金屬-級數二:神之金屬")) {
								case 1:
									mes "^3355FF您試圖使用木材來修復";
									mes "但他並非現在所需要的材料";
									mes "使得情況更為惡化了.";
									mes "必須從頭開始.^000000";
									close;
								case 2:
									mes "^3355FF你用鋼材焊接";
									mes "所有的裂痕被你焊接牢固";
									mes "防禦線已經被修復了.^000000";
									set .@rp_temp,.@rp_temp+1;
									set .@ro_of02,.@ro_of02+1;
									specialeffect2 EF_REPAIRWEAPON;
									next;
									break;
								case 3:
									mes "^3355FF您試圖使用強化武器金屬修復,";
									mes "但他並非現在所需要的材料";
									mes "你必須在嘗試使用";
									mes "其他的材料來修復防禦線.^000000";
									close;
								case 4:
									mes "^3355FF您試圖使用神之金屬修復,";
									mes "但他並非現在所需要的材料";
									mes "你必須在嘗試使用";
									mes "其他的材料來修復防禦線.^000000";
									close;
								}
								break;
							case 4:
								mes "^3355FF現在你需要";
								mes "重新建立一道防禦線";
								mes "所需材料:強化武器金屬.^000000";
								next;
								switch(select("瑪哪樹枝:鋼鐵:強化武器金屬-級數二:神之金屬")) {
								case 1:
									mes "^3355FF您試圖使用木材來修復";
									mes "但他並非現在所需要的材料";
									mes "使得情況更為惡化了.";
									mes "必須從頭開始.^000000";
									close;
								case 2:
									mes "^3355FF您試圖使用鋼鐵修復,";
									mes "但他並非現在所需要的材料";
									mes "你必須在嘗試使用";
									mes "其他的材料來修復防禦線.^000000";
									close;
								case 3:
									mes "強化武器金屬修復完成";
									mes "把防禦線的防禦都恢復了";
									set .@rp_temp,.@rp_temp+1;
									set .@ro_of02,.@ro_of02+1;
									specialeffect2 EF_REPAIRWEAPON;
									next;
									break;
								case 4:
									mes "^3355FF您試圖使用神之金屬修復,";
									mes "但他並非現在所需要的材料";
									mes "你必須在嘗試使用";
									mes "其他的材料來修復防禦線.^000000";
									close;
								}
							}
						}
					}
					mes "^3355FF它看起來像已經損壞";
					mes "你必須要準備";
					mes "修復防禦線.^000000";
					next;
					if (!agitcheck2()) {
						mes "^3355FF不幸的是,城堡的";
						mes "防禦線不能重建:";
						mes "這裡的防禦線還是沒有.^000000";
						close;
					}
					else {
						if (.@rp_temp == .@ro_of01) {
							mes "^3355FF該防禦線";
							mes "已成功\修復!^000000";
							delitem 1019,30; //Wooden_Block
							delitem 999,10; //Steel
							delitem 1011,10; //Emveretarcon
							delitem 984,5; //Oridecon
							close2;
							donpcevent "RL"+.@num+"#"+strnpcinfo(4)+"::OnEnable";
							disablenpc();
							if (.@num == 1) set .@str$,"1st";
							else if (.@num == 2) set .@str$,"2nd";
							else if (.@num == 3) set .@str$,"3rd";
							mapannounce strnpcinfo(4),"第 "+.@str$+" 防禦線 重新建置成功\了!",bc_map,"0x00ff00";
							if (.@num == 1) setd .@var$+"[2]",0;
							else {
								setarray getd(.@var$+"["+.@num+"]"),2,0;
								donpcevent "控制裝置0"+(.@num-1)+"#"+strnpcinfo(2)+"::OnEnable";
							}
							end;
						}
						else {
							mes "^3355FF防禦線已經損壞,";
							mes "你試圖修復他";
							mes "但很可惜的失敗了.";
							mes "材料已經修復部分";
							delitem 984,2; //Oridecon
							delitem 999,4; //Steel
							delitem 1019,14; //Wooden_Block
							delitem 1011,3; //Emveretarcon
							close;
						}
					}
				}
				else {
					mes "所需材料不足";
					close;
				}
			}
		}
	}
	end;

OnInit:
OnDisable:
	disablenpc();
	end;

OnEnable:
	enablenpc();
	end;
}

// Guardian Summoners (2)
//============================================================
-	script	gard#template	-1,{
OnEnable:
	// .@x[i],.@y[i]: Normal coordinates, #0-21.
	// .@w[x],.@w[y]: Special coordinates if 'defence' is under 11.
	if (compare(strnpcinfo(2),"arug")) {
		if (strnpcinfo(2) == "arug_cas01") {
			setarray .@w[0],195,250,292,188;
			setarray .@x[0],233,252,232,201,224,196,269,252,201,224,222, 294,256,240,246,235,235,246,240,256,254,242;
			setarray .@y[0], 83, 81,108,130,168,137, 89, 81,130,168,129, 210,203,133, 92,132,132, 92,133,203, 95,151;
		}
		else if (strnpcinfo(2) == "arug_cas02") {
			setarray .@w[0],20,169,268,169;
			setarray .@x[0],104,67,67,113,122,67, 90, 91,122, 20,67, 175,204,211,209,161,186,183,150,161,209,211;
			setarray .@y[0], 32,36,85, 87,112,60,167,119,112,169,85,  31, 32, 63, 88, 91,170,121,110, 91, 88, 63;
		}
		else {	// Castles 3,4,5 are identical.
			setarray .@w[0],66,157,211,159;
			setarray .@x[0],130,128,128,128,110,91,65, 65,110,128,128, 156,172,154,156,155,187,212,211,155,156,172;
			setarray .@y[0], 60, 77, 90,100, 96,53,71,103, 96,100, 77, 101, 95, 90, 77, 60, 54, 67,105, 60, 77, 95;
		}
	}
	else {
		if (strnpcinfo(2) == "schg_cas02") {
			setarray .@w[0],337,95,307,222;
			setarray .@x[0],326,337,334,296,285,236,285,296,334,337,334, 359,300,337,317,307,300,337,317,307,359,236;
			setarray .@y[0], 83, 95,119, 82, 40, 41, 40, 82,119, 95,119,  85,119,154,183,222,119,154,183,222, 85, 41;
		}
		else if (strnpcinfo(2) == "schg_cas03") {
			setarray .@w[0],306,325,364,305;
			setarray .@x[0],323,273,288,306,323,323,273,288,306,273,273, 338,364,365,317,338,338,364,365,317,364,329;
			setarray .@y[0],308,309,306,326,308,308,309,306,325,309,309, 309,305,261,318,310,309,305,261,318,305,314;
		}
		else {	// Castles 1,4,5 are identical.
			setarray .@w[0],108,32,128,42,187,15;	// Contains an extra pair, for whatever reason.
			setarray .@x[0],111,109,65,110,88,64,47,109,111,112,120, 130,129,151,187,128,152,187,128,130,130,151;
			setarray .@y[0], 18, 44,22, 40,20,40,43, 48, 18, 32, 37,  22, 47, 18, 15, 42, 43, 15, 42, 22, 28, 18;
		}
	}
	if (charat(strnpcinfo(1),4) == "2") set .@z,11;
	freeloop(1);
	set .@defence,getcastledata(strnpcinfo(2),CD_CURRENT_DEFENSE);
	callsub OnSummon,.@z;
	if (.@defence > 70) set getd(".MyMobCount_"+charat(strnpcinfo(1),4)+strnpcinfo(2)),5;
	else if (.@defence > 50) set getd(".MyMobCount_"+charat(strnpcinfo(1),4)+strnpcinfo(2)),4;
	else if (.@defence > 30) set getd(".MyMobCount_"+charat(strnpcinfo(1),4)+strnpcinfo(2)),3;
	else if (.@defence > 10) set getd(".MyMobCount_"+charat(strnpcinfo(1),4)+strnpcinfo(2)),2;
	if (.@w[4] && .@z)
		guardian strnpcinfo(2),.@w[4],.@w[5],"Guardian Soldier",1899,strnpcinfo(0)+"::OnGuardianDied";
	else if (.@defence < 11) {
		set getd(".MyMobCount_"+charat(strnpcinfo(1),4)+strnpcinfo(2)),2;
		set .@i,(.@z)?2:0;
		guardian strnpcinfo(2),.@w[.@i],.@w[.@i+1],"Guardian Soldier",1899,strnpcinfo(0)+"::OnGuardianDied";
	}
	else for(set .@i,1; .@i<getd(".MyMobCount_"+charat(strnpcinfo(1),4)+strnpcinfo(2)); set .@i,.@i+1)
		callsub OnSummon,.@i+.@z;
	freeloop(0);
	copyarray getd(".x_"+strnpcinfo(2)+"[0]"),.@x[0],22;
	copyarray getd(".y_"+strnpcinfo(2)+"[0]"),.@y[0],22;
	setd ".timer_"+charat(strnpcinfo(1),4)+strnpcinfo(2),4+.@z;
	setarray .count$[5],"1st","2nd","3rd","4th","5th";
	initnpctimer;
	end;

OnTimer300000:
OnTimer900000:
OnTimer1800000:
OnTimer2700000:
OnTimer3600000:
	if (charat(strnpcinfo(1),4) == "2") end;
	set .@var$,".timer_"+charat(strnpcinfo(1),4)+strnpcinfo(2);
	setd .@var$, getd(.@var$)+1;
	set getd(".MyMobCount_"+charat(strnpcinfo(1),4)+strnpcinfo(2)),getd(".MyMobCount_"+charat(strnpcinfo(1),4)+strnpcinfo(2))+1;
	callsub OnSummon,getd(.@var$);
	setarray .count$[5],"1st","2nd","3rd","4th","5th";
	mapannounce strnpcinfo(2),"The "+.count$[getd(.@var$)]+" Guardian has been summoned from the Gate House.",bc_map,"0xff4500";
	if (getd(.@var$) == 9) {
		setd .@var$,0;
		stopnpctimer;
	}
	end;

OnTimer600000:
OnTimer1200000:
OnTimer2100000:
OnTimer3000000:
OnTimer3900000:
	if (!(charat(strnpcinfo(1),4) == "2")) end;
	set .@var$,".timer_"+charat(strnpcinfo(1),4)+strnpcinfo(2);
	setd .@var$, getd(.@var$)+1;
	set getd(".MyMobCount_"+charat(strnpcinfo(1),4)+strnpcinfo(2)),getd(".MyMobCount_"+charat(strnpcinfo(1),4)+strnpcinfo(2))+1;
	callsub OnSummon,getd(.@var$);
	if (getd(.@var$) == 20) {
		setd .@var$,0;
		stopnpctimer;
	}
	end;

OnSummon:
	guardian strnpcinfo(2),getd(".x_"+strnpcinfo(2)+"["+getarg(0)+"]"),getd(".y_"+strnpcinfo(2)+"["+getarg(0)+"]"),"首領 監護人",1899,strnpcinfo(0)+"::OnGuardianDied";
	return;

OnGuardianDied:
	if (charat(strnpcinfo(1),4) == "2") set .@z,11;
	set getd(".MyMobCount_"+charat(strnpcinfo(1),4)+strnpcinfo(2)),getd(".MyMobCount_"+charat(strnpcinfo(1),4)+strnpcinfo(2))-1;
	if (getd(".MyMobCount_"+charat(strnpcinfo(1),4)+strnpcinfo(2)) < 2) {
		set getd(".MyMobCount_"+charat(strnpcinfo(1),4)+strnpcinfo(2)),getd(".MyMobCount_"+charat(strnpcinfo(1),4)+strnpcinfo(2))+1;
		callsub OnSummon,10+.@z;
	}
	end;

OnReset:
	stopnpctimer;
	killmonster strnpcinfo(2),strnpcinfo(0)+"::OnGuardianDied";
	deletearray getd(".x_"+strnpcinfo(2)+"[0]"),22;
	deletearray getd(".y_"+strnpcinfo(2)+"[0]"),22;
	end;
}

// Guardian Stone Summoners (2)
//============================================================
-	script	df#template	-1,{
OnEnable:
	if (compare(strnpcinfo(2),"arug")) {
		if (strnpcinfo(2) == "arug_cas01") setarray .@i[0],210,234,308,189;
		else if (strnpcinfo(2) == "arug_cas02") setarray .@i[0],33,168,245,168;
		else setarray .@i[0],65,171,212,149;	// Castles 3,4,5 are identical.
	}
	else {
		if (strnpcinfo(2) == "schg_cas02") setarray .@i[0],231,58,335,230;
		else if (strnpcinfo(2) == "schg_cas03") setarray .@i[0],242,309,376,251;
		else setarray .@i[0],27,35,207,75;	// Castles 1,4,5 are identical.
	}
	set .@num, atoi(charat(strnpcinfo(1),2));
	set .@j,(.@num == 1)?0:2;
	guardian strnpcinfo(2),.@i[.@j],.@i[.@j+1],((.@num == 1)?"1st":"2nd")+" Guardian Stone",1906+.@num,strnpcinfo(0)+"::OnGuardianStoneDied";
	end;

OnDisable:
	killmonster strnpcinfo(2),strnpcinfo(0)+"::OnGuardianStoneDied";
	setd "$agit_"+substr(strnpcinfo(2),0,1)+substr(strnpcinfo(2),8,9)+"["+(atoi(charat(strnpcinfo(1),2))-1)+"]",1;
	stopnpctimer;
	end;

OnGuardianStoneDied:
	set .@num, atoi(charat(strnpcinfo(1),2));
	set .@var$,"$agit_"+substr(strnpcinfo(2),0,1)+substr(strnpcinfo(2),8,9);
	setd .@var$+"["+(.@num-1)+"]",1;
	if (getd(.@var$+"[0]") == 1 || getd(.@var$+"[0]") == 2) set .@destroyed, .@destroyed+1;
	if (getd(.@var$+"[1]") == 1 || getd(.@var$+"[1]") == 2) set .@destroyed, .@destroyed+1;
	if (.@destroyed == 2) {
		mapannounce strnpcinfo(2),"All of the Guardian Stones have been destroyed!",bc_map,"0x00ff00";
		donpcevent "RL0#"+strnpcinfo(2)+"::OnDisable";
	}
	else mapannounce strnpcinfo(2),"The "+((.@num == 1)?"1st":"2nd")+" Guardian Stone has been destroyed!",bc_map,"0x00ff00";
	donpcevent "gard"+.@num+"#"+strnpcinfo(2)+"::OnReset";
	initnpctimer;
	end;

OnTimer300000:
	set .@num, atoi(charat(strnpcinfo(1),2));
	set .@str$,substr(strnpcinfo(2),0,1)+substr(strnpcinfo(2),8,9);
	donpcevent ((.@num == 1)?"1st":"2nd")+" Guardian Stone#"+.@str$+"::OnEnable";
	setd "$agit_"+.@str$+"["+(atoi(charat(strnpcinfo(1),2))-1)+"]",2;
	stopnpctimer;
	end;
}

// Barrier Summoners (4)
//============================================================
-	script	RL#template	-1,{
OnEnable:
	.@npc_name$ = strnpcinfo(0);
	.@npc_visible$ = strnpcinfo(1);
	.@npc_hidden$ = strnpcinfo(2);
	set .@num, atoi(charat(.@npc_visible$,2));
	if (.@num == 0) {
		if (compare(.@npc_hidden$,"arug")) {
			if (.@npc_hidden$ == "arug_cas01") {
				setarray .@wall[0],238,74,8,6,0;
				setarray .@x[0],239,241,243,245;
				setarray .@y[0], 73, 73, 73, 73;
			}
			else if (.@npc_hidden$ == "arug_cas02") {
				setarray .@wall[0],136,136,8,6,0;
				setarray .@x[0],137,139,141,143;
				setarray .@y[0],137,137,137,137;
			}
			else {	// Castles 3,4,5 are identical.
				setarray .@wall[0],138,110,8,6,0;
				setarray .@x[0],139,141,143,145;
				setarray .@y[0],111,111,111,111;
			}
		}
		else {
			if (.@npc_hidden$ == "schg_cas02") {
				setarray .@wall[0],290,98,8,0,0;
				setarray .@x[0],289,289,289,289;
				setarray .@y[0], 98,100,102,104;
			}
			else if (.@npc_hidden$ == "schg_cas03") {
				setarray .@wall[0],326,301,6,6,0;
				setarray .@x[0],326,328,330;
				setarray .@y[0],300,300,300;
			}
			else {	// Castles 1,4,5 are identical.
				setarray .@wall[0],114,48,13,6,0;
				setarray .@x[0],115,117,119,121,123,125;
				setarray .@y[0], 49, 49, 49, 49, 49, 49;
			}
		}
	}
	else if (.@num == 1) {
		if (compare(.@npc_hidden$,"arug")) {
			if (.@npc_hidden$ == "arug_cas01") {
				setarray .@wall[0],239,53,8,6,1;
				setarray .@x[0],239,241,243,240,242,244;
				setarray .@y[0], 55, 55, 55, 54, 54, 54;
			}
			else if (.@npc_hidden$ == "arug_cas02") {
				setarray .@wall[0],150,223,12,6,1;
				setarray .@x[0],151,153,155,157,159,161;
				setarray .@y[0],222,222,222,222,222,222;
			}
			else {	// Castles 3,4,5 are identical.
				setarray .@wall[0],139,158,6,6,1;
				setarray .@x[0],140,142,144,139,141,143;
				setarray .@y[0],157,157,157,156,156,156;
			}
		}
		else {
			if (.@npc_hidden$ == "schg_cas02") {
				setarray .@wall[0],279,98,8,0,1;
				setarray .@x[0],280,280,280,281,281,281;
				setarray .@y[0], 98,100,102, 99,101,103;
			}
			else if (.@npc_hidden$ == "schg_cas03") {
				setarray .@wall[0],325,277,8,6,1;
				setarray .@x[0],326,328,330,327,329,331;
				setarray .@y[0],278,278,278,279,279,279;
			}
			else {	// Castles 1,4,5 are identical.
				setarray .@wall[0],114,51,13,6,1;
				setarray .@x[0],115,117,119,121,123,125;
				setarray .@y[0], 50, 50, 50, 50, 50, 50;
			}
		}
	}
	else if (.@num == 2) {
		if (compare(.@npc_hidden$,"arug")) {
			if (.@npc_hidden$ == "arug_cas01") {
				setarray .@wall[0],107,124,6,6,1;
				setarray .@x[0],107,109,111,108,110,112;
				setarray .@y[0],122,122,122,123,123,123;
			}
			else if (.@npc_hidden$ == "arug_cas02") {
				setarray .@wall[0],125,342,8,0,1;
				setarray .@x[0],126,126,126,127,127,127;
				setarray .@y[0],343,345,347,344,346,348;
			}
			else {	// Castles 3,4,5 are identical.
				setarray .@wall[0],138,210,8,6,1;
				setarray .@x[0],140,142,144,139,141,143;
				setarray .@y[0],209,209,209,208,208,208;
			}
		}
		else {
			if (.@npc_hidden$ == "schg_cas02") {
				setarray .@wall[0],230,213,6,0,1;
				setarray .@x[0],231,231,231,232,232,232;
				setarray .@y[0],213,215,217,213,215,217;
			}
			else if (.@npc_hidden$ == "schg_cas03") {
				setarray .@wall[0],200,230,8,0,1;
				setarray .@x[0],201,201,201,202,202,202;
				setarray .@y[0],231,233,235,232,234,236;
			}
			else {	// Castles 1,4,5 are identical.
				setarray .@wall[0],114,154,13,6,1;
				setarray .@x[0],115,117,119,121,123,125;
				setarray .@y[0],153,153,153,153,153,153;
			}
		}
	}
	else {
		if (compare(.@npc_hidden$,"arug")) {
			if (.@npc_hidden$ == "arug_cas01") {
				setarray .@wall[0],84,171,8,6,1;
				setarray .@x[0], 84, 86, 88, 90;
				setarray .@y[0],170,170,170,170;
			}
			else if (.@npc_hidden$ == "arug_cas02") {
				setarray .@wall[0],38,314,12,6,1;
				setarray .@x[0], 40, 42, 44, 46;
				setarray .@y[0],315,315,315,315;
			}
			else {	// Castles 3,4,5 are identical.
				setarray .@wall[0],138,263,8,6,1;
				setarray .@x[0],139,141,143,145;
				setarray .@y[0],262,262,262,262;
			}
		}
		else {
			if (.@npc_hidden$ == "schg_cas02") {
				setarray .@wall[0],160,141,6,6,1;
				setarray .@x[0],160,162,164,166;
				setarray .@y[0],140,140,140,140;
			}
			else if (.@npc_hidden$ == "schg_cas03") {
				setarray .@wall[0],285,198,8,0,1;
				setarray .@x[0],284,284,284,284;
				setarray .@y[0],199,201,203,205;
			}
			else {	// Castles 1,4,5 are identical.
				setarray .@wall[0],116,241,11,6,1;
				setarray .@x[0],116,118,120,122;
				setarray .@y[0],240,240,240,240;
			}
		}
	}
	setwall .@npc_hidden$,.@wall[0],.@wall[1],.@wall[2],.@wall[3],.@wall[4],substr(.@npc_hidden$,0,1)+substr(.@npc_hidden$,8,9)+"_"+.@npc_visible$;
	switch(.@num) {
	case 0:
		killmonster .@npc_hidden$, .@npc_name$ + "::OnBarrierDestroyed";
		.@j = getarraysize(.@x);
		for ( .@i = 0; .@i < .@j; ++.@i ) {
			.@unit_id = guardian( .@npc_hidden$,.@x[.@i],.@y[.@i]," ",1905, .@npc_name$ + "::OnBarrierDestroyed" );
			setunitdata .@unit_id,UMOB_DMGIMMUNE,1; // The 1st barricades are immunes to damage until both Guardian Stones are destroyed
		}
		end;
	case 3:
		setd ".MyMobCount_" + .@num + .@npc_hidden$, 4;
		break;
	default:
		setd ".MyMobCount_" + .@num + .@npc_hidden$, 6;
		break;
	}
	for ( .@i = 0; .@i < getd( ".MyMobCount_" + .@num + .@npc_hidden$ ); ++.@i )
		guardian .@npc_hidden$,.@x[.@i],.@y[.@i]," ",1905, .@npc_name$ + "::OnBarrierDestroyed";
	end;

OnBarrierDestroyed:
	set .@num, atoi(charat(strnpcinfo(1),2));
	if (!.@num) end;
	set getd(".MyMobCount_"+.@num+strnpcinfo(2)),getd(".MyMobCount_"+.@num+strnpcinfo(2))-1;
	if (getd(".MyMobCount_"+.@num+strnpcinfo(2)) == 0) {
		set .@var$,substr(strnpcinfo(2),0,1)+substr(strnpcinfo(2),8,9);
		setd "$agit_"+.@var$+"["+(.@num+1)+"]",1;
		setarray .@count$[0],"1st","2nd","3rd";
		mapannounce strnpcinfo(2),"The "+.@count$[.@num-1]+" Fortress Gate is destroyed.",bc_map,"0x00ff00";
		delwall .@var$+"_"+strnpcinfo(1);
	}
	end;

OnDisable:
	.@wall_name$ = substr(strnpcinfo(2),0,1) + substr(strnpcinfo(2),8,9) + "_" + strnpcinfo(1);
	if (checkwall(.@wall_name$) == true)
		delwall .@wall_name$;
	killmonster strnpcinfo(2),strnpcinfo(0)+"::OnBarrierDestroyed";
	end;
}

// Link Flags (function)
//============================================================
function	script	LinkFlag	{
	if (!getcharid(2) || getcharid(2) != getcastledata(strnpcinfo(4),CD_GUILD_ID)) end;
	if (getarg(0) == "Convenience Facility") {
		mes "-秘密基地內部 空間移動系統-";
		mes "可以移動到為了會員們的便利所形成的區域,";
		mes "請問你要移動嗎?";
		if(select("移動到便利的區域:取消") == 1)
			warp strnpcinfo(4),getarg(1),getarg(2);
		close;
	}
	if (getarg(0) == "Emperium Center") {
		mes "-秘密基地內部 空間移動系統-";
		mes "移動到有華麗金屬中央的戰略地區,";
		mes "請問你要使用嗎?";
		if(select("移動:取消") == 1)
			warp strnpcinfo(4),getarg(1),getarg(2);
		close;
	}
	mes "-秘密基地內部 空間移動系統-";
	mes "請選擇你要去的地方";
	for(set .@i,0; .@i<getargcount(); set .@i,.@i+3)
		set .@menu$, .@menu$+getarg(.@i)+":";
	set .@menu$, .@menu$+"Cancel";
	set .@i, select(.@menu$)-1;
	if (.@i != getargcount()/3)
		warp strnpcinfo(4),getarg(.@i*3+1),getarg(.@i*3+2);
	close;
}

// Return Flags (function)
//============================================================
function	script	ReturnFlag	{
	.@map$ = getarg(0);
	set .@str$, (compare(strnpcinfo(4),"aru"))?"Arunafeltz":"Schwarzwald";
	.@GID =  getcastledata(.@map$,CD_GUILD_ID);
	if (!.@GID) {
		mes "[ "+.@str$+" 教國 公告令 ]";
		mes "The Holy Kingdom of";
		mes .@str$+" declares that";
		mes "one has yet to claim lordship";
		mes "over this stronghold. The one";
		mes "that breaks the Emperium will";
		mes "be recognized as its new owner.";
		close;
	}
	if (getcharid(2) == .@GID && getarg(1,0)) {
		mes "[ 響亮的聲音 ]";
		mes "勇敢者們...";
		mes "你要回到你的 那名譽的時刻嗎?";
		next;
		if(select("Return to the Stronghold:Cancel") == 1 && getcharid(2) == getcastledata(.@map$,CD_GUILD_ID)) {
			if (compare(.@map$,"arug")) {
				if (.@map$ == "arug_cas01") setarray .@i[0],67,193;
				else if (.@map$ == "arug_cas02") setarray .@i[0],43,256;
				else setarray .@i[0],121,318;	// Castles 3,4,5 are identical.
			}
			else {
				if (.@map$ == "schg_cas02") setarray .@i[0],136,188;
				else if (.@map$ == "schg_cas03") setarray .@i[0],308,202;
				else setarray .@i[0],120,290;	// Castles 1,4,5 are identical.
			}
			warp .@map$,.@i[0],.@i[1];
		}
		close;
	}
	mes "[ "+.@str$+" 教國 公告令 ]";
	mes "The Holy Kingdom of";
	mes .@str$+" decrees that";
	mes "this stronghold is owned";
	mes "by the ^FF0000"+getguildname(.@GID)+"^000000 Guild.";
	next;
	mes "[ "+.@str$+" Royal Edict ]";
	mes "^FF0000"+getguildmaster(.@GID)+"^000000 is";
	mes "Guild Master of ^FF0000"+getguildname(.@GID)+"^000000.";
	mes "Any that object must claim this";
	mes "stronghold through strength of";
	mes "steel and magic during the";
	mes "appointed Guild Siege times.";
	close;
}

// Treasure Room Switches
//============================================================
-	script	Switch#template	-1,{

		mes "無法使用這個出去!";
		close;
	mes " ";
	mes "有個某種小的裝置,請問你要操作嗎?";
	next;
	if(select("Pull Lever:Cancel") == 2) close;
	if (compare(strnpcinfo(4),"arug")) {
		if (strnpcinfo(4) == "arug_cas01") setarray .@i[0],121,357;
		else if (strnpcinfo(4) == "arug_cas02") setarray .@i[0],387,323;
		else setarray .@i[0],321,57;	// Castles 3,4,5 are identical.
	}
	else {
		if (strnpcinfo(4) == "schg_cas02") setarray .@i[0],339,79;
		else if (strnpcinfo(4) == "schg_cas03") setarray .@i[0],57,13;
		else setarray .@i[0],275,244;	// Castles 1,4,5 are identical.
	}
	warp strnpcinfo(4),.@i[0],.@i[1];
	close;
}

// Guild Dungeon Warps
//============================================================
-	script	Sunflower#template	-1,{
	if (getcharid(2) == getcastledata(strnpcinfo(4),CD_GUILD_ID)) {
		mes "- It's an amazingly huge sunflower; as big as a human! ... You feel something mysterious emanating from the flower. -";
		next;
		switch(select("Hold the stem.:Do nothing.")) {
		case 1:
			if (compare(strnpcinfo(4),"arug")) {
				set .@map$,"arug_dun01";
				setarray .@mapx[0],350,350,50, 50,200;
				setarray .@mapy[0],350, 50,50,350,386;
			}
			else {
				set .@map$,"schg_dun01";
				setarray .@mapx[0],262, 94, 79,212,322;
				setarray .@mapy[0],314,284,140, 70,166;
			}
			set .@i, atoi(charat(strnpcinfo(4),9))-1;
			warp .@map$,.@mapx[.@i],.@mapy[.@i];
			close;
		case 2:
			mes "It's too scary to touch unknown things.";
			close;
		}
	}
}
