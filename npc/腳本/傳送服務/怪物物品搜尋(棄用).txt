
function	script	mob_finding	{

	callfunc "搜尋傳送";
	close;
}

function	script	item_finding	{
Page1:
	clear;
	mes "^5599FF       物品搜尋";
	mes "卡片:^CC3300橙色";
	mes "其他:^000055藍色";
	mes "裝備:^00AA00綠色";
	mes "消耗:^FF0000紅色";
	mes "進階搜尋:物品名稱+空格+種類";
	mes "^0000FF鋁 裝備=^00AA00鋁合金鎧甲";
	.@menu$ = "查詢:";
	for( .@i = 0 ; .@i < 10 ; .@i ++){
		.@menu$ += item_lookup$[.@i] + ":";
	}
	.@sdiffa = select(.@menu$);
	
	if(.@sdiffa == 1){
		clear;
		mes "^5599FF       物品搜尋";
		mes "請輸入物品ID/名稱:";
		mes "卡片:^CC3300橙色";
		mes "其他:^000055藍色";
		mes "裝備:^00AA00綠色";
		mes "消耗:^FF0000紅色";	
		mes "進階搜尋:物品名稱+空格+種類";
		mes "^0000FF鋁 裝備=^00AA00鋁合金鎧甲";
		.@menu$ = "查詢:";		
		input .@itemname$;
	}else{
		.@itemname$ = item_lookup$[.@sdiffa-2];
	}
	.@itemtemp$ = .@itemname$;
	explode(.@itemname$,.@itemname$," ");
Page2:
	clear;
	mes "^5599FF       物品搜尋";
	mes "請輸入物品ID/名稱:^5599FF" + .@itemtemp$;
	mes "請選取清單物品:";
	mes "卡片:^CC3300橙色";
	mes "其他:^000055藍色";
	mes "裝備:^00AA00綠色";
	mes "消耗:^FF0000紅色";
	if (getarraysize(.@itemname$) >1){
		.@itemresult$ = "";
		if( .@itemname$[1] == "卡片"){
			.@itemresult$ = "and typenum in (0)";
		}else if(.@itemname$[1] == "其他"){
			.@itemresult$ = "and typenum in (1,2,3,4)";
		}else if(.@itemname$[1] == "裝備"){
			.@itemresult$ = "and typenum in (5,6,7,8)";
		}else if(.@itemname$[1] == "消耗"){
			.@itemresult$ = "and typenum in (9,10,11)";
		}
		.@nb = query_sql ("SELECT DISTINCT `AEGISNAME`, `NAME` , `ID` , `TYPE` FROM ITEM_DB_RE WHERE (`ID` = '"+.@itemname$+"' or `aegisname` like '%"+.@itemname$+"%' or `name` like '%"+.@itemname$+"%') "+.@itemresult$+" order by typenum,length(name),ID LIMIT 35",.@aegis_name$,.@item_name$,.@item_id,.@itemtype$);
		
	}else{
		.@nb = query_sql ("SELECT DISTINCT `AEGISNAME`, `NAME` , `ID` , `TYPE` FROM ITEM_DB_RE WHERE `ID` = '"+.@itemname$+"' or `aegisname` like '%"+.@itemname$+"%' or `name` like '%"+.@itemname$+"%' order by typenum,length(name),ID LIMIT 35",.@aegis_name$,.@item_name$,.@item_id,.@itemtype$);
	}
	.@itemnamelist$ = "";
	for( .@i = 0 ; .@i < getarraysize(.@item_name$) ; .@i++){
		//.@itemnamelist$ += .@item_name$[.@i] + " " + .@aegis_name$[.@i]+":";
		if(.@itemtype$[.@i] == "Healing" || .@itemtype$[.@i] == "Delayconsume" || .@itemtype$[.@i] == "Usable"){
			.@itemnamelist$ += "^FF0000"+.@item_name$[.@i] + " ("+.@item_id[.@i]+"):";
		}else if(.@itemtype$[.@i] == "Weapon" || .@itemtype$[.@i] == "Armor" || .@itemtype$[.@i] == "Shadowgear" || .@itemtype$[.@i] == "Petarmor"){
			.@itemnamelist$ += "^00AA00"+.@item_name$[.@i] + " ("+.@item_id[.@i]+"):";
		}else if(.@itemtype$[.@i] == "Card"){
			.@itemnamelist$ += "^CC3300"+.@item_name$[.@i] + " ("+.@item_id[.@i]+"):";
		}else{
			.@itemnamelist$ += "^000055"+.@item_name$[.@i] + " ("+.@item_id[.@i]+"):";
		}
		
	}
	.@item_name$[.@i] = "^0000FF上一頁";
	.@itemnamelist$ +="^0000FF上一頁";
	.@sdiffaa = select(.@itemnamelist$);
	if(.@item_name$[.@sdiffaa-1] =="^0000FF上一頁"){
		deletearray .@aegis_name$;
		deletearray .@itemname$;
		deletearray .@item_name$;
		deletearray .@typename$;
		deletearray .@item_id;
		deletearray .@itemtype$;
		goto Page1;
	}
	
Page3:
	clear;
	mes "^5599FF       物品搜尋";
	mes "請輸入物品ID/名稱:^5599FF" + .@itemtemp$;
	mes "請選取清單物品:" + .@item_name$[.@sdiffaa-1];
	.@aegis_name$[.@sdiffaa-1] = replacestr(.@aegis_name$[.@sdiffaa-1],"'","''");
	.@nb = query_sql("SELECT DISTINCT B.ID,B.JAPANESENAME,B.ISBOSS,D.DROPRATE,C.TYPE FROM MAPINFO A,MOB_DB_RE B ,ITEM_DB_RE C, MOB_DROP D WHERE C.aegisname = '"+.@aegis_name$[.@sdiffaa-1]+"' and B.ID = A.MOBID AND B.ID = D.ID AND C.AEGISNAME = D.AEGISNAME ORDER BY B.ISBOSS,D.DROPRATE DESC,LENGTH(B.JAPANESENAME) LIMIT 25",.@mob_id,.@mob_name$,.@isboss,.@drop_rate,.@item_type$);
	.@aegis_name$[.@sdiffaa-1] = replacestr(.@aegis_name$[.@sdiffaa-1],"''","'");
	.@mobnamelist$ = "";
	
	if (getarraysize(.@mob_name$) == 0){
		mes "^FF0000此物品沒有怪物掉落，可能是製作或其他取得途徑";
	}else{
		mes "請選取清單怪物:";
		mes "一般怪物:黑色";
		mes "BOSS怪物:^0000FF藍色";
		mes "MVP 怪物:^FF0000紅色";
	}
	for( .@i = 0 ; .@i < getarraysize(.@mob_name$) ; .@i++){
		
		switch(.@isboss[.@i]){
			case 0:
				if( .@item_type$[.@i] == "Card"){
					.@br = getbattleflag ("item_rate_card") / 100;
				}else if( .@item_type$[.@i] == "Healing" || .@item_type$[.@i] == "Delayconsume" || .@item_type$[.@i] == "Usable"){
					.@br = getbattleflag ("item_rate_use") / 100;
				}else if( .@item_type$[.@i] == "Weapon" || .@item_type$[.@i] == "Armor" || .@item_type$[.@i] == "Petarmor" || .@item_type$[.@i] == "Shadowgear"){
					.@br = getbattleflag ("item_rate_equip") / 100;
				}else if( .@item_type$[.@i] == "Etc" || .@item_type$[.@i] == "Ammo" || .@item_type$[.@i] == "Petegg"){
					.@br = getbattleflag ("item_rate_common") / 100;
				}
				.@drop_rate[.@i] = min(.@drop_rate[.@i] * .@br,10000);
				.@mobnamelist$ += "^000000"+.@mob_name$[.@i] + " "+.@drop_rate[.@i]/100 + ((.@drop_rate[.@i]%100 < 10) ? ".0":".") + .@drop_rate[.@i]%100 + "%:";
			
				break;
			case 1:
				if( .@item_type$[.@i] == "Card"){
					.@br = getbattleflag ("item_rate_card_boss") / 100;
				}else if( .@item_type$[.@i] == "Healing" || .@item_type$[.@i] == "Delayconsume" || .@item_type$[.@i] == "Usable"){
					.@br = getbattleflag ("item_rate_use_boss") / 100;
				}else if( .@item_type$[.@i] == "Weapon" || .@item_type$[.@i] == "Armor" || .@item_type$[.@i] == "Petarmor" || .@item_type$[.@i] == "Shadowgear"){
					.@br = getbattleflag ("item_rate_equip_boss") / 100;
				}else if( .@item_type$[.@i] == "Etc" || .@item_type$[.@i] == "Ammo" || .@item_type$[.@i] == "Petegg"){
					.@br = getbattleflag ("item_rate_common_boss") / 100;
				}
				.@drop_rate[.@i] = min(.@drop_rate[.@i] * .@br,10000);
				.@mobnamelist$ += "^0000FF"+.@mob_name$[.@i] + " "+.@drop_rate[.@i]/100 + ((.@drop_rate[.@i]%100 < 10) ? ".0":".") + .@drop_rate[.@i]%100 + "%:";
			
				break;
			case 2:
				if( .@item_type$[.@i] == "Card"){
					.@br = getbattleflag ("item_rate_card_mvp") / 100;
				}else if( .@item_type$[.@i] == "Healing" || .@item_type$[.@i] == "Delayconsume" || .@item_type$[.@i] == "Usable"){
					.@br = getbattleflag ("item_rate_use_mvp") / 100;
				}else if( .@item_type$[.@i] == "Weapon" || .@item_type$[.@i] == "Armor" || .@item_type$[.@i] == "Petarmor" || .@item_type$[.@i] == "Shadowgear"){
					.@br = getbattleflag ("item_rate_equip_mvp") / 100;
				}else if( .@item_type$[.@i] == "Etc" || .@item_type$[.@i] == "Ammo" || .@item_type$[.@i] == "Petegg"){
					.@br = getbattleflag ("item_rate_common_mvp") / 100;
				}
				.@drop_rate[.@i] = min(.@drop_rate[.@i] * .@br,10000);
				.@mobnamelist$ += "^FF0000"+.@mob_name$[.@i] + " "+.@drop_rate[.@i]/100 + ((.@drop_rate[.@i]%100 < 10) ? ".0":".") + .@drop_rate[.@i]%100 + "%:";
			
				break;
		}
		/*
		if (.@isboss[.@i] == 2){
			.@drop_rate[.@i] = min(.@drop_rate[.@i] * 10,10000);
			.@mobnamelist$ += "^FF0000"+.@mob_name$[.@i] + " "+.@drop_rate[.@i]/100 + ((.@drop_rate[.@i]%100 < 10) ? ".0":".") + .@drop_rate[.@i]%100 + "%:";
		}else if(.@isboss[.@i] == 1){
			.@drop_rate[.@i] = min(.@drop_rate[.@i] * 10,10000);
			.@mobnamelist$ += "^0000FF"+.@mob_name$[.@i] + " "+.@drop_rate[.@i]/100 + ((.@drop_rate[.@i]%100 < 10) ? ".0":".") + .@drop_rate[.@i]%100 + "%:";
		
		}else{
			if(.@item_type$[.@i] == "Card"){
				.@drop_rate[.@i] = min(.@drop_rate[.@i]* 100,10000);
			}else{
				.@drop_rate[.@i] = min(.@drop_rate[.@i]* 50,10000);
			}
		
			.@mobnamelist$ += "^000000"+.@mob_name$[.@i] + " "+.@drop_rate[.@i]/100 + ((.@drop_rate[.@i]%100 < 10) ? ".0":".") + .@drop_rate[.@i]%100 + "%:";
		}*/
		
	}
	.@mob_name$[.@i] = "^0000FF上一頁";
	.@mobnamelist$ +="^0000FF上一頁";
	.@sdiffb = select(.@mobnamelist$);
	if(.@mob_name$[.@sdiffb-1] =="^0000FF上一頁"){
		deletearray .@aegis_name$;
		deletearray .@item_name$;
		deletearray .@mob_id$;
		deletearray .@mob_name$;
		deletearray .@isboss;
		goto Page2;
	}
	mes .@mob_name$[.@sdiffb-1];
Page4:
	clear;
	mes "^5599FF       物品搜尋";
	mes "請輸入物品ID/名稱:^5599FF" + .@itemtemp$;
	mes "請選取清單物品:" + .@item_name$[.@sdiffaa-1];
	mes "請選取清單怪物:^008800" + .@mob_name$[.@sdiffb-1];
	
	if (getmobdrops(.@mob_id[.@sdiffb-1])) {	// 'getmobdrops' returns 1 on success
		.@count = $@MobDrop_count;
		copyarray .@item[0],$@MobDrop_item[0],.@count;
		copyarray .@rate[0],$@MobDrop_rate[0],.@count;
		
		for( .@i = 0; .@i < .@count; .@i++ ) {
			//mes .@item[.@i] + " (" + getitemname(.@item[.@i]) + ") " + .@rate[.@i]/100 + ((.@rate[.@i]%100 < 10) ? ".0":".") + .@rate[.@i]%100 + "%";
			if(.@rate[.@i] <= 100){
				mes "^AAAA33"+getitemname(.@item[.@i]) + "- " + .@rate[.@i]/100 + ((.@rate[.@i]%100 < 10) ? ".0":".") + .@rate[.@i]%100 + "%";
			}else{
				mes "^000000"+getitemname(.@item[.@i]) + "- " + .@rate[.@i]/100 + ((.@rate[.@i]%100 < 10) ? ".0":".") + .@rate[.@i]%100 + "%";
			}
		}
	}
	
	
	
	mes "請選擇傳送地圖:";
	.@nb = query_sql("SELECT A.mapId,A.mobcnt,C.MAPNAME FROM MAPINFO A,MOB_DB_RE B,MAPINFO_NAME C WHERE A.MAPID = C.MAPID AND A.MOBID = B.id AND B.JAPANESENAME = '"+.@mob_name$[.@sdiffb-1] + "' ORDER BY A.MOBCNT DESC",.@map_id$,.@mobcnt,.@map_name$);
	.@mapidlist$ = "";
	for( .@i = 0 ; .@i < getarraysize(.@map_id$) ; .@i++){
		.@mapidlist$ += .@map_name$[.@i] +" ("+ .@map_id$[.@i] + ") "+.@mobcnt[.@i]+"隻:";
	}
	.@map_name$[.@i] += "^0000FF上一頁";
	.@mapidlist$ += "^0000FF上一頁";
	.@sdiffc = select(.@mapidlist$);

	
	if(.@map_name$[.@sdiffc-1] =="^0000FF上一頁"){
		deletearray .@mob_id$;
		deletearray .@mob_name$;
		deletearray .@isboss;
		deletearray .@mobcnt;
		deletearray .@map_id$;
		deletearray .@map_name$;
		goto Page3;
	}

Page5:
	clear;
	mes "^5599FF       物品搜尋";
	mes "請輸入物品ID/名稱:^5599FF" + .@itemtemp$;
	mes "請選取清單物品:" + .@item_name$[.@sdiffaa-1];
	mes "請選取清單怪物:^008800" + .@mob_name$[.@sdiffb-1];
	mes "請選擇傳送地圖:^880000" + .@map_name$[.@sdiffc-1];
	
	.@nb = query_sql("select b.japanesename,a.mobcnt,b.isboss from mapinfo A, mob_db_re B where a.mapid = '"+.@map_id$[.@sdiffc-1]+"' and a.mobid = b.id order by b.isboss desc, a.mobcnt desc",.@mmob_name$,.@mmob_cnt,.@misboss);

	for( .@i = 0 ; .@i < getarraysize(.@mmob_name$) ; .@i++){
		if(.@misboss[.@i] == 2){
			mes "^FF0000"+.@mmob_name$[.@i] + "^000000 - " + .@mmob_cnt[.@i] +" 隻";
		}else if (.@misboss[.@i] == 1){
			mes "^0000FF"+.@mmob_name$[.@i] + "^000000 - " + .@mmob_cnt[.@i] +" 隻";
		}else{
			mes .@mmob_name$[.@i] + " - " + .@mmob_cnt[.@i] +" 隻";
		}
	}


	
	switch(select("^FF0000傳送:^0000FF上一頁")){
		case 1:
			if(.@sdiffa == 1){
				for( .@i = 9 ; .@i > 0 ; .@i--){
					item_lookup$[.@i] = item_lookup$[.@i-1];
				}
				item_lookup$[0] = .@item_name$[.@sdiffaa-1];
			}else{
				.@temp$ = .@item_name$[.@sdiffaa-1];
				for( .@i = .@sdiffa -2 ; .@i >0 ; .@i--){
					item_lookup$[.@i] = item_lookup$[.@i-1];
				}
				item_lookup$[0] = .@temp$;
			}
			warp .@map_id$[.@sdiffc-1],0,0;
			end;	
		case 2:
			deletearray .@mobcnt;
			deletearray .@map_id$;
			deletearray .@map_name$;
			deletearray .@mmob_name$;
			deletearray .@mmob_cnt;
			deletearray .@misboss;
			goto Page4;
	}
	close;
}