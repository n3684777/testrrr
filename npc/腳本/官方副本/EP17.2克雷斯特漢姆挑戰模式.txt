//===== Giraffe 模擬器 - Script ===================
//**********作者: [ Leo ]**************************
// 聯繫方式: https://discord.gg/JVeaMmkRZD
//===== Description: ==============================
//
//*************************************************
//===================請保留以上資訊================

1@gl_he	mapflag	noteleport
1@gl_he	mapflag	nosave	SavePoint
1@gl_he	mapflag	nomemo
1@gl_he	mapflag	nobranch
1@gl_he	mapflag	noicewall
1@gl_he	mapflag	restricted	6
1@gl_he	mapflag	monster_noteleport
1@gl_he	mapflag	partylock
1@gl_he	mapflag	novending
1@gl_he	mapflag	nochat

vdistrict1,7,39,4	script	克雷斯特漢姆挑戰模式管理者	10307,{

	//危險的挑戰任務獎勵
	if(checkquest(12495,HUNTING)==2 ) { 
	mes "[奧斯卡]";
	mes "^0000FF封印的時間頭冠^0000001個";
	mes "這個是從闇答萊屍身上取得的戰利品。";
	next;
	getitem 25864,1*$EP1702;	//封印的時間頭冠
	erasequest 12495;
	}
	if(checkquest(12496,HUNTING)==2 ) { 
	mes "[奧斯卡]";
	mes "^0000FF封印的時間頭冠^0000001個";
	mes "這個是從希梅爾茲身上取得的戰利品。";
	next;
	getitem 25864,1*$EP1702;	//封印的時間頭冠
	erasequest 12496;
	}

	//副本任務獎勵1
	if(checkquest(32022,HUNTING)==2 ) {
	dispbottom "克雷斯特漢姆挑戰模式任務完成，以下是你的獎勵!!"; 
	set #Instance_Points,#Instance_Points+20;	//副本積分+20
	dispbottom "副本積分+20";
	dispbottom "目前副本積分 ^0000ff"+#Instance_Points+"^000000 點";
	getitem 61006,10;	//商城1點金幣
	erasequest 32022; }
	//副本任務獎勵2
	if(checkquest(32026,HUNTING)==2 ) {
	dispbottom "克雷斯特漢姆挑戰模式任務完成，以下是你的獎勵!!"; 
	set #Instance_Points,#Instance_Points+20;	//副本積分+20
	dispbottom "副本積分+20";
	dispbottom "目前副本積分 ^0000ff"+#Instance_Points+"^000000 點";
	getitem 61006,10;	//商城1點金幣
	erasequest 32026; }

OnStart:
	//挑戰次數
	if ( day_Instance_Bout_39 < gettime(8) ){
	set Instance_Bout_39,10;
	set day_Instance_Bout_39,gettime(8);	//一年中的一天
	end; }
	if (Instance_Bout_39 < 1) {
		mes "挑戰次數不足";
		close;
	}

	set .@party_id,getcharid(1);
	set .@p_name$,getpartyname(.@party_id);

	if (!.@party_id) {
		mes "[奧斯卡]";
		mes "你的隊伍成員呢?";
		close;
	}
	
	.@md_name$ = "克雷斯特漢姆挑戰模式";
	
	if (getcharid(0) == getpartyleader(.@party_id,2))
		set @menu$, "創建"+.@md_name$+":進入"+.@md_name$;
	else
		set @menu$, ":進入"+.@md_name$;
		
	mes "[奧斯卡]";
	mes "副本獎勵倍數[ ^0000FF"+$EP1702+"^000000 ]";
	mes "挑戰剩餘次數[ ^FF0000"+Instance_Bout_39+"^000000 ]";
	mes "副本獎勵：";
	mes "封印的時間頭冠";
	mes "超時空寶石原石";
	mes "超時空魔力";
	mes "超時空魔力碎片";
	mes "商城1點金幣10";
	mes "官方副本積分x20";
	mes "你想要幹嘛呢？";
	next;
	.@i = select(@menu$)-1;
	switch(.@i) {
	case 0:
		if(BaseLevel < 170) {
			mes "[奧斯卡]";
			mes "裡面是很危險的…"; 
			mes "還是等到 Lv.170 後再來吧…"; 
			close;
		}
		
		if (instance_create(.@md_name$) < 0) {
			mes "[奧斯卡]";
			mes "隊伍名稱: "+.@p_name$;
			mes "隊伍隊長: "+strcharinfo(0);
			mes "^0000ff"+.@md_name$+" ^000000- 創建失敗!";
			close;
		}

		mapannounce strnpcinfo(4),"隊伍 "+getpartyname(.@party_id)+" 準備挑戰【"+.@md_name$+"】副本！",bc_map,"0x33ea91";	
		mes "[奧斯卡]";
		mes ""+.@md_name$+"已經創立了∼";
		mes "請選擇^0066CC進入"+.@md_name$+"^000000。";
		
		.@iid = instance_id(IM_PARTY);
		set getinstancevar('party_id, .@iid), getcharid(1);
		//入場記錄時間
		set Instance_Annal,gettimetick(2);
		close;
	case 1:
		if(BaseLevel < 170) {
			mes "[奧斯卡]";
			mes "裡面是很危險的…"; 
			mes "還是等到 Lv.170 後再來吧…"; 
			close;
		}
	if ( getinstancevar('Win,instance_id(IM_PARTY)) ) {
	mes "副本已經開始了，無法再次進入。";
	close;
	}
		switch(instance_enter(.@md_name$)) {
		case IE_OTHER:
			mes "不明錯誤。";
			close;
		case IE_NOINSTANCE:
			mes .@md_name$+" 副本不存在。";
			mes "隊長尚未申請記憶迷宮。";
			close;
		case IE_NOMEMBER:
			mes "只有申請 "+.@md_name$+" 副本的隊員才可以進入。";
			close;
		case IE_OK:
			//mapannounce strnpcinfo(4),"隊伍 " + getpartyname(.@party_id) + " 裡的勇者 " + strcharinfo(0) + " 開始進行【"+.@md_name$+"】副本了",bc_map,"0x00ff99";			
			//扣除挑戰次數
			set Instance_Bout_39,Instance_Bout_39-1;

			//副本任務
			if(checkquest(32022,HUNTING)<0 ){setquest 32022;}
			if(checkquest(32026,HUNTING)<0 ){setquest 32026;}
			if(checkquest(12495,HUNTING)<0 ){setquest 12495;}
			if(checkquest(12496,HUNTING)<0 ){setquest 12496;}
		//禁止多開偵測
		if(getsameipinfo(getcharip(), strcharinfo(3)) > $Windows){
		//允許多開名單
		for(set .@a,0; .@a < getarraysize($@PartyWindowsows$); set .@a,.@a+1){
		if(getcharip()==$@PartyWindowsows$[.@a]){
		end; }}
		mes "道館跟副本禁止多開!";
		mes "檢測到副本地圖中有相同IP人物";
		mes "^777777提示：3秒後自動傳送回首都^000000";
		sleep2 3000;
		warp "prontera",102,118;
		end; }
			close;
		}
	}
	close;
	
OnInit:
	waitingroom "克雷斯特漢姆挑戰模式",0;
	end;
}


//============================================================
1@gl_he,149,45,5	script	奧斯卡#oscar_1	4_ED_OSCAR,{
	if(getpartyleader(getcharid(1),1) == getcharid(3)) 
	{

		cutin "oscar01",2;
		mes "[奧斯卡]";
		mes "請仔細的選擇您要挑戰的難度";
		mes "根據選擇的階段出現的魔物強度也會有所不同";
		mes "獎勵也是不盡相阿";
		mes "清除的時間也會影響副本的流程";
		next;
		set .@typeChoice, select("我想再等一下", "選擇汙染魔力階段等級");
		switch(.@typeChoice) {
		case 1:
			mes "[奧斯卡]";
			mes "等你好再跟我說吧！。";
			cutin "gl_heinrich1",255;
			close;
		case 2:
			set .@typeset ,#CGHTYPE;
			if (.@typeset == 0){ set .@typeset , 1; set #CGHTYPE,1;}
			for ( .@i = 1; .@i <= .@typeset; .@i++ ) {
				  .@menu$ += "汙染魔力第[" + .@i + "]階段";
				  .@menu$ += ":";
			}
			'selectdiff = select(.@menu$);
			break;
		}
		mes "[奧斯卡]";
		mes "您選擇為第["+'selectdiff +"]階段";
		mes "請問是否正確？";
		next;
		if(select("不對", "是的，沒有錯。") == 1) {
			mes "[奧斯卡]";
			mes "請重新再選擇一次吧！";
			cutin "gl_heinrich1",255;
			close;
		}
		donpcevent instance_npcname("#CGH_ghmemorialmob05")+"::OnEnable";
		donpcevent instance_npcname("#CGH_ghmemorialmob08")+"::OnEnable";
		donpcevent instance_npcname("#checkkill")+"::OnEnable";
		set .@partpeople,getmapusers(instance_mapname("1@gl_he"));
		set 'warppeople,.@partpeople/2;
		set 'warppeopleto1,0;
		set 'warppeopleto2,0;
		donpcevent instance_npcname("奧斯卡#oscar_1")+"::OnPrize1";
		disablenpc instance_npcname("奧斯卡#oscar_1");
		cutin "gl_heinrich1",255;
		end;
	}else{
		mes "[奧斯卡]";
		mes "麻煩請叫隊長來找我。";
	}
	close;
	
	OnPrize1://組隊平均分散傳送
		addrid(4,0,166,12,133,43);
		if( rand(2) ){
			warp instance_mapname("1@gl_he"),126,123;
			set 'warppeopleto1,'warppeopleto1+1;
				if ('warppeopleto1 > 'warppeople){
					warp instance_mapname("1@gl_he"),167,140;
					set 'warppeopleto2,'warppeopleto2+1;
				}
		}else{
			warp instance_mapname("1@gl_he"),167,141;
			set 'warppeopleto2,'warppeopleto2+1;
				if ('warppeopleto2 > 'warppeople){
					warp instance_mapname("1@gl_he"),126,123;
					set 'warppeopleto1,'warppeopleto1+1;
				}
		}
	end;
}

//----傳點設置------
1@gl_he,135,124,0	warp	LeftWarps#face	1,1,1@gl_he,150,69
1@gl_he,146,122,0	warp	midLeftWarps#face	1,1,1@gl_he,126,123
1@gl_he,164,140,0	warp	RightWarps#face	1,1,1@gl_he,150,69
1@gl_he,154,100,0	warp	midRightWarps#face	1,1,1@gl_he,167,141
1@gl_he,150,163,0	warp	Toboss#face	1,1,1@gl_he,150,168

1@gl_he,145,124,0	warp	傳送門#Gl_he_01	2,2,1@gl_he,127,124
1@gl_he,134,124,0	warp	傳送門#Gl_he_02	2,2,1@gl_he,151,124
1@gl_he,154,101,0	warp	傳送門#Gl_he_03	2,2,1@gl_he,173,101
1@gl_he,165,101,0	warp	傳送門#Gl_he_04	2,2,1@gl_he,149,101

//------------------

1@gl_he,0,0,0	script	#CGH_ghmemorialmob05	-1,{//左方區域
OnInstanceInit:
	hideonnpc instance_npcname("奧斯卡#oscar_2");
	hideonnpc instance_npcname("奧斯卡#oscar_3");
	hideonnpc instance_npcname("寶箱#chatnmtop");
	hideonnpc instance_npcname("奧斯卡#oscar_5");
	hideonnpc instance_npcname("奧斯卡#oscar_4");
	disablenpc instance_npcname("LeftWarps#face");
	disablenpc instance_npcname("RightWarps#face");
	disablenpc instance_npcname("midLeftWarps#face");
	disablenpc instance_npcname("midRightWarps#face");
	disablenpc instance_npcname("Toboss#face");
OnDisable:
	disablenpc instance_npcname("#CGH_ghmemorialmob05");
	end;
OnEnable:
	enablenpc instance_npcname("#CGH_ghmemorialmob05");
	set .@map$, instance_mapname("1@gl_he");
	set .@label$, instance_npcname("#CGH_ghmemorialmob05")+"::OnMyMobDead";


	mapannounce .@map$, "奧斯卡：為了消除這個空間的污染魔力，首先我們得先淨化兩側空間受汙染的幻影魔物。",bc_map,"0xFFFF00";
	mapannounce .@map$, "奧斯卡：我會告訴你們還有多少數量的受汙染的幻影需要淨化。",bc_map,"0xFFFF00";
	
	areamonster .@map$,124,20,31,162,"--ja--",20574,10,.@label$;
	areamonster .@map$,124,20,31,162,"--ja--",20576,10,.@label$;
	areamonster .@map$,124,20,31,162,"--ja--",20577,10,.@label$;
	areamonster .@map$,124,20,31,162,"--ja--",20578,10,.@label$;
	areamonster .@map$,124,20,31,162,"--ja--",20579,10,.@label$;
	areamonster .@map$,124,20,31,162,"--ja--",20579,10,.@label$;
	set 'MyMobs2,50;
	end;

OnMyMobDead:
	if ('MyMobs2 < 0) {end;}
	set 'MyMobs2, 'MyMobs2-1;
	set .@map$, instance_mapname("1@gl_he");
	areaannounce .@map$,124,20,31,162,""+'MyMobs2+" <- 左側未淨化的幻影 -> "+'MyMobs ,bc_map,"0xFF8800";

	if ('MyMobs2 == 0) {
		areaannounce .@map$,124,20,31,162, "奧斯卡：右側的幻影尚未淨化完成，通往右邊區域的傳送點已經開啟了。",bc_map,"0xFFFF00";
		enablenpc instance_npcname("LeftWarps#face");
		enablenpc instance_npcname("midRightWarps#face");
		donpcevent instance_npcname("#checkkill")+"::OnEnd";
		donpcevent instance_npcname("奧斯卡#oscar_1")+"::OnPrize1";
		
	}
	end;
}


1@gl_he,0,0,0	script	#CGH_ghmemorialmob08	-1,{
OnInstanceInit:
OnDisable:
	disablenpc instance_npcname("#CGH_ghmemorialmob08");
	end;
OnEnable:
	enablenpc instance_npcname("#CGH_ghmemorialmob08");
	set .@map$, instance_mapname("1@gl_he");
	set .@label$, instance_npcname("#CGH_ghmemorialmob08")+"::OnMyMobDead";
	areamonster .@map$,175,163,265,18,"--ja--",20574,10,.@label$;
	areamonster .@map$,175,163,265,18,"--ja--",20576,10,.@label$;
	areamonster .@map$,175,163,265,18,"--ja--",20577,10,.@label$;
	areamonster .@map$,175,163,265,18,"--ja--",20578,10,.@label$;
	areamonster .@map$,175,163,265,18,"--ja--",20579,10,.@label$;
	areamonster .@map$,175,163,265,18,"--ja--",20579,10,.@label$;
	set 'MyMobs,50;
	end;

OnMyMobDead:
	if ('MyMobs < 0) {end;}
	set 'MyMobs, 'MyMobs-1;
	set .@map$, instance_mapname("1@gl_he");
	areaannounce .@map$,157,165,269,18,'MyMobs2 +"<- 右側未淨化的幻影 -> "+'MyMobs,bc_map,"0xFF8800";

	if ('MyMobs == 0) {
		areaannounce .@map$,175,163,265,18, "奧斯卡：左側的幻影尚未淨化完成，通往左邊區域的傳送點已經開啟了。",bc_map,"0xFFFF00";
		enablenpc instance_npcname("RightWarps#face");
		enablenpc instance_npcname("midLeftWarps#face");
		donpcevent instance_npcname("#checkkill")+"::OnEnd";
	}
	end;
}

//-----------判定兩邊清除結束--------------
1@gl_he,172,206,4	script	#checkkill	-1,{
OnEnable:
	enablenpc instance_npcname("#checkkill");
	set 'checkkillcheck,2;
	end;
OnEnd:
	set 'checkkillcheck,'checkkillcheck-1;
	if('checkkillcheck){
	}else{

		set .@map$, instance_mapname("1@gl_he");
		disablenpc instance_npcname("midLeftWarps#face");
		disablenpc instance_npcname("midRightWarps#face");
		mapannounce .@map$, "奧斯卡：淨化完成，通往中間區域的傳送點已經開啟了。",bc_map,"0xFFFF00";
		mapannounce .@map$, "奧斯卡：到達後請前往12點鐘方向移動並找到我。",bc_map,"0xFFFF00";
		hideoffnpc instance_npcname("奧斯卡#oscar_2");
		areamonster .@map$,150,158,150,158,"--ja--",20575,1;
	}
	end;
}


1@gl_he,150,163,1	script	#bossdoor	139,{
end;
OnEnable:
	initnpctimer;
	specialeffect 831;
	end;
OnTimer3000:
	specialeffect 135;
	enablenpc instance_npcname("#bossdoor");
	hideonnpc instance_npcname("#bossdoor");
	stopnpctimer;
	end;
}

1@gl_he,150,158,1	script	奧斯卡#oscar_2	4_ED_OSCAR,{
	if(getpartyleader(getcharid(1),1) == getcharid(3)) {
		cutin "oscar01",2;
		mes "[奧斯卡]";
		mes "如果國王問起的話.";
		mes "這也是沒有辦法的事情，我只能去做了";
		next;
		mes "[奧斯卡]";
		mes "我們剩沒有多少時間了";
		mes "別忘記我們來的目的.";
		next;
		if(select("等等！", "變形") == 1) {
			mes "[奧斯卡]";
			mes "好了再跟我說一下。";
			cutin "gl_heinrich1",255;
			close;
		}
		cutin "gl_heinrich1",255;
		close2;
		
		npctalk "(變形中)";
		donpcevent instance_npcname("#bossdoor")+"::OnEnable";
		progressbar_npc "ffff00",2;
		specialeffect 521;
		specialeffect 215;
		setnpcdisplay strnpcinfo(3), "奧斯卡", 1713;
		npctalk "這就是我的真面目";
		initnpctimer;


		end;
	}else{
		mes "[奧斯卡]";
		mes "麻煩請叫隊長來找我。";
	}
	close;
	
	OnTimer1000:
	npcspeed 200;
	npcwalkto 150,162;
	end;
	OnTimer2000:
	enablenpc instance_npcname("Toboss#face");
	hideonnpc instance_npcname("奧斯卡#oscar_2");
	specialeffect 215;
	stopnpctimer;
	donpcevent instance_npcname("奧斯卡#oscar_3")+"::OnEnable";
	end;
}

1@gl_he,150,169,1	script	奧斯卡#oscar_3	4_ED_OSCAR,{
end;
OnEnable:
	hideoffnpc instance_npcname("奧斯卡#oscar_3");
	initnpctimer;
	setnpcdisplay strnpcinfo(3), "奧斯卡", 1713;
	end;
OnTimer3000:
	npcspeed 200;
	npcwalkto 150,199;
	end;
OnTimer9000:
	npcspeed 200;
	npcwalkto 157,229;
	end;
OnTimer15500:
	specialeffect 215;
	stopnpctimer;
	hideonnpc instance_npcname("奧斯卡#oscar_3");
	hideoffnpc instance_npcname("奧斯卡#oscar_4");
	donpcevent instance_npcname("#bosseffect")+"::OnEnable";
	end;
}

1@gl_he,157,240,1	script	#bosseffect	139,{
end;
OnEnable:
	specialeffect 838;
	specialeffect 1085;
	end;
OnStrat:
	initnpctimer;
	specialeffect 1066;
	end;
OnTimer3000:
	specialeffect 840;
	specialeffect 1068;
	stopnpctimer;
	end;
}
//==================BOSS召喚==============
1@gl_he,157,229,1	script	奧斯卡#oscar_4	4_ED_OSCAR,{
	if(getpartyleader(getcharid(1),1) == getcharid(3)) {
		cutin "oscar01",2;
		mes "[奧斯卡]";
		mes "眼前這模糊不清的東西，";
		mes "就是扭曲時間裡魔力的汙染源";
		next;
		mes "[奧斯卡]";
		mes "現在我會將它重新淨化.";
		mes "如果進行的順利的話，他就會出現在這裡";
		next;
		mes "[奧斯卡]";
		mes "請特別注意.";
		mes "這個汙染源是非常強大的，";
		mes "務必隨時保持警惕。";
		next;
		mes "[奧斯卡]";
		mes "如果準備好了，請讓我知道.";
		next;
		if(select("等等！", "我們準備好了") == 1) {
			mes "[奧斯卡]";
			mes "好了再跟我說一下。";
			cutin "gl_heinrich1",255;
			close;
		}
		cutin "gl_heinrich1",255;
		close2;
		npctalk "汗染等級"+'selectdiff+"的幻影即將出現了，我需要牠在中間的魔法陣裡才能將他淨化，請想辦法將他困在中間區域。";
		donpcevent instance_npcname("#bosseffect")+"::OnStrat";
		initnpctimer;
		progressbar_npc "ffff00",5;
		donpcevent instance_npcname("#CGH_BOSS")+"::OnEnable";
		donpcevent instance_npcname("#bossarea")+"::OnEnable";
		

		end;
	}else{
		mes "[奧斯卡]";
		mes "麻煩請叫隊長來找我。";
	}
	close;
	OnTimer4000:
	specialeffect 236;
	end;
	OnTimer5000:
	hideonnpc instance_npcname("奧斯卡#oscar_4");
	end;
	OnTimer6000:
	mapwarp instance_mapname("1@gl_he"),instance_mapname("1@gl_he"),157,227;
	stopnpctimer;
	set .@map$,instance_mapname("1@gl_he");
	mapannounce .@map$, "奧斯卡：闇答萊屍的幻影的魔力會使我們在這空間中述失自我，請務必要小心對付！",bc_map,"0xFFFF00";
	unittalk 'BossID, "你是我的新主人嗎？";
	end;
}

1@gl_he,2,3,4	script	#CGH_BOSS	-1,{

OnEnable:
	enablenpc instance_npcname("#CGH_BOSS");
	donpcevent instance_npcname("#CGH_BOSS")+"::OnStart";
	end;

OnStart:
	set .@map$,instance_mapname("1@gl_he");
	set .@killboss$, instance_npcname("#CGH_BOSS")+"::OnKillMob";
	if ('selectdiff < 7){
		monster .@map$,157,239,"闇答萊屍",20573,1,instance_npcname("#CGH_BOSS")+"::OnKillMob";
	}
	else{
		monster .@map$,157,239,"希梅爾茲",20572,1,instance_npcname("#CGH_BOSS")+"::OnKillMob";
	}

	'BossID = $@mobid[0];
	initnpctimer;
	'bossf2 = 0;
	
	//設定汙染階級血量
	getunitdata 'BossID,'amdaraisdata;
	setarray .@AMDARAIS[0],600000000,750000000,900000000,1050000000,1200000000,1350000000,1500000000,1650000000,1800000000,2000000000;
	setarray .@HEMEL[0],600000000,750000000,900000000,1050000000,1200000000,1350000000,1500000000,1650000000,1800000000,2000000000;
	setarray .@bosshp[0],600000000,750000000,900000000,1050000000,1200000000,1350000000,1500000000,1650000000,1800000000,2000000000;

	setunitdata 'BossID,3,.@bosshp['selectdiff -1];
	end;
	
OnKillMob:
	if ('fail == 0){
	set .@map$,instance_mapname("1@gl_he");
	mapannounce .@map$, "奧斯卡：闇答萊屍的幻影已經淨化完成",bc_map,"0xFFFF00";
	donpcevent instance_npcname("#CGH_BOSS")+"::Onstoptime";
	donpcevent instance_npcname("#bossarea")+"::Onstoptime";
	hideoffnpc instance_npcname("寶箱#chatnmtop");
	hideoffnpc instance_npcname("奧斯卡#oscar_5");
	'bossf2 = 1;
	stopnpctimer;
	}
	end;
	
Onrandwarp:
	addrid(4,0,30,267,265,18);
		if(rand(100) > 65){
		warp instance_mapname("1@gl_he"),0,0;
		}
	end;


Onstoptime:
	stopnpctimer;
	end;
	
Onstarttime:
	initnpctimer;
	end;


OnTimer5000:
	if ('bossf2 != 1){
	getunitdata 'BossID,'amdaraisdata;
	.@label$ = instance_npcname("#CGH_BOSS") + "::OnSkillunit";
	.@x = 'amdaraisdata[6];
	.@y = 'amdaraisdata[7];
	.@skill = rand(1,6);

	set .@map$,instance_mapname("1@gl_he");
	switch(.@skill) {
	case 1:  //廣範圍
		unittalk 'BossID, "廣範圍詛咒";
		sleep 1000;
		monster .@map$,.@x+4,.@y-4,"--ja--",3095,1,.@label$;		
		.@id[0] = $@mobid[0];
		monster .@map$,.@x+8,.@y-4,"--ja--",3095,1,.@label$;
		.@id[1] = $@mobid[0];
		monster .@map$,.@x+4,.@y+4,"--ja--",3095,1,.@label$;
		.@id[2] = $@mobid[0];
		monster .@map$,.@x+8,.@y+4,"--ja--",3095,1,.@label$;
		.@id[3] = $@mobid[0];
		for(.@i = 0; .@i < 4; .@i++) {
			unitskilluseid .@id[.@i], 353, 1;
		}
		sleep 500;
		for(.@i = 0; .@i <= 18; .@i=.@i+2){
			if(unitexists(.@id[0]) == true)
				unitskillusepos .@id[0],83,5,.@x+.@i,.@y+.@i,-10;
			if(unitexists(.@id[1]) == true)
				unitskillusepos .@id[1],83,5,.@x+.@i,.@y-.@i,-10;
			if(unitexists(.@id[2]) == true)
				unitskillusepos .@id[2],83,5,.@x-.@i,.@y-.@i,-10;
			if(unitexists(.@id[3]) == true)
				unitskillusepos .@id[3],83,5,.@x-.@i,.@y+.@i,-10;
			sleep 500;
		}
		sleep 1000;
			break;
	case 2: //十字地震術
		unittalk 'BossID, "!！";
		sleep 1000;
		monster .@map$,.@x+9,.@y+3,"--ja--",3095,1,.@label$;
		.@id[0] = $@mobid[0];
		monster .@map$,.@x-9,.@y-3,"--ja--",3095,1,.@label$;
		.@id[1] = $@mobid[0];
		monster .@map$,.@x-3,.@y+9,"--ja--",3095,1,.@label$;
		.@id[2] = $@mobid[0];
		monster .@map$,.@x+3,.@y-9,"--ja--",3095,1,.@label$;
		.@id[3] = $@mobid[0];
		for(.@i = 0; .@i < 4; .@i++) {
			setunitdata .@id[.@i], 29, 1;
			unitskilluseid .@id[.@i], 353, 1;
		}
		sleep 500;
			if(unitexists(.@id[0]) == true)
				unitskillusepos .@id[0],91,5,.@x+2,.@y,-5;
			if(unitexists(.@id[1]) == true)
				unitskillusepos .@id[1],91,5,.@x-2,.@y,-5;
			if(unitexists(.@id[2]) == true)
				unitskillusepos .@id[2],91,5,.@x,.@y+2,-5;
			if(unitexists(.@id[3]) == true)
				unitskillusepos .@id[3],91,5,.@x,.@y-2,-5;
		sleep 10;
			if(unitexists(.@id[0]) == true)
				unitskillusepos .@id[0],91,5,.@x+6,.@y,-5;
			if(unitexists(.@id[1]) == true)
				unitskillusepos .@id[1],91,5,.@x-6,.@y,-5;
			if(unitexists(.@id[2]) == true)
				unitskillusepos .@id[2],91,5,.@x,.@y+6,-5;
			if(unitexists(.@id[3]) == true)
				unitskillusepos .@id[3],91,5,.@x,.@y-6,-5;
			if(unitexists(.@id[0]) == true)
				unitskillusepos .@id[0],91,5,.@x+4,.@y+4,-5;
			if(unitexists(.@id[1]) == true)
				unitskillusepos .@id[1],91,5,.@x-4,.@y-4,-5;
			if(unitexists(.@id[2]) == true)
				unitskillusepos .@id[2],91,5,.@x-4,.@y+4,-5;
			if(unitexists(.@id[3]) == true)
				unitskillusepos .@id[3],91,5,.@x+4,.@y-4,-5;
		sleep 10;
			if(unitexists(.@id[0]) == true)
				unitskillusepos .@id[0],91,5,.@x+9,.@y,-5;
			if(unitexists(.@id[1]) == true)
				unitskillusepos .@id[1],91,5,.@x-9,.@y,-5;
			if(unitexists(.@id[2]) == true)
				unitskillusepos .@id[2],91,5,.@x,.@y+9,-5;
			if(unitexists(.@id[3]) == true)
				unitskillusepos .@id[3],91,5,.@x,.@y-9,-5;
			if(unitexists(.@id[0]) == true)
				unitskillusepos .@id[0],91,5,.@x+7,.@y+7,-5;
			if(unitexists(.@id[1]) == true)
				unitskillusepos .@id[1],91,5,.@x-7,.@y-7,-5;
			if(unitexists(.@id[2]) == true)
				unitskillusepos .@id[2],91,5,.@x-7,.@y+7,-5;
			if(unitexists(.@id[3]) == true)
				unitskillusepos .@id[3],91,5,.@x+7,.@y-7,-5;
		sleep 10;
			if(unitexists(.@id[0]) == true)
				unitskillusepos .@id[0],91,5,.@x+13,.@y,-5;
			if(unitexists(.@id[1]) == true)               
				unitskillusepos .@id[1],91,5,.@x-13,.@y,-5;
			if(unitexists(.@id[2]) == true)               
				unitskillusepos .@id[2],91,5,.@x,.@y+13,-5;
			if(unitexists(.@id[3]) == true)               
				unitskillusepos .@id[3],91,5,.@x,.@y-13,-5;
			if(unitexists(.@id[0]) == true)
				unitskillusepos .@id[0],91,5,.@x+10,.@y+10,-5;
			if(unitexists(.@id[1]) == true)                  
				unitskillusepos .@id[1],91,5,.@x-10,.@y-10,-5;
			if(unitexists(.@id[2]) == true)                  
				unitskillusepos .@id[2],91,5,.@x-10,.@y+10,-5;
			if(unitexists(.@id[3]) == true)                  
				unitskillusepos .@id[3],91,5,.@x+10,.@y-10,-5;
		sleep 10;
			if(unitexists(.@id[0]) == true)
				unitskillusepos .@id[0],91,5,.@x+16,.@y,-5;
			if(unitexists(.@id[1]) == true)               
				unitskillusepos .@id[1],91,5,.@x-16,.@y,-5;
			if(unitexists(.@id[2]) == true)               
				unitskillusepos .@id[2],91,5,.@x,.@y+16,-5;
			if(unitexists(.@id[3]) == true)               
				unitskillusepos .@id[3],91,5,.@x,.@y-16,-5;
			if(unitexists(.@id[0]) == true)
				unitskillusepos .@id[0],91,5,.@x+13,.@y+13,-5;
			if(unitexists(.@id[1]) == true)                  
				unitskillusepos .@id[1],91,5,.@x-13,.@y-13,-5;
			if(unitexists(.@id[2]) == true)                  
				unitskillusepos .@id[2],91,5,.@x-13,.@y+13,-5;
			if(unitexists(.@id[3]) == true)                  
				unitskillusepos .@id[3],91,5,.@x+13,.@y-13,-5;
		sleep 1000;
		break;
	case 3: //擴散地震
		unittalk 'BossID, "怒爆！！";
		sleep 500;
		monster .@map$,.@x+9,.@y+3,"--ja--",3095,1,.@label$;
		.@id[0] = $@mobid[0];
		monster .@map$,.@x-9,.@y-3,"--ja--",3095,1,.@label$;
		.@id[1] = $@mobid[0];
		monster .@map$,.@x-3,.@y+9,"--ja--",3095,1,.@label$;
		.@id[2] = $@mobid[0];
		monster .@map$,.@x+3,.@y-9,"--ja--",3095,1,.@label$;
		.@id[3] = $@mobid[0];
		for(.@i = 0; .@i < 4; .@i++) {
			setunitdata .@id[.@i], 29, 1;
			unitskilluseid .@id[.@i], 353, 1;
		}
		sleep 500;
			if(unitexists(.@id[0]) == true)
				unitskillusepos .@id[0],91,5,.@x+2,.@y,-5;
			if(unitexists(.@id[1]) == true)              
				unitskillusepos .@id[1],91,5,.@x-2,.@y,-5;
			if(unitexists(.@id[2]) == true)              
				unitskillusepos .@id[2],91,5,.@x,.@y+2,-5;
			if(unitexists(.@id[3]) == true)              
				unitskillusepos .@id[3],91,5,.@x,.@y-2,-5;
		sleep 10;
			if(unitexists(.@id[0]) == true)
				unitskillusepos .@id[0],91,5,.@x+6,.@y,-5;
			if(unitexists(.@id[1]) == true)              
				unitskillusepos .@id[1],91,5,.@x-6,.@y,-5;
			if(unitexists(.@id[2]) == true)              
				unitskillusepos .@id[2],91,5,.@x,.@y+6,-5;
			if(unitexists(.@id[3]) == true)              
				unitskillusepos .@id[3],91,5,.@x,.@y-6,-5;
			if(unitexists(.@id[0]) == true)
				unitskillusepos .@id[0],91,5,.@x+4,.@y+4,-5;
			if(unitexists(.@id[1]) == true)                
				unitskillusepos .@id[1],91,5,.@x-4,.@y-4,-5;
			if(unitexists(.@id[2]) == true)                
				unitskillusepos .@id[2],91,5,.@x-4,.@y+4,-5;
			if(unitexists(.@id[3]) == true)                
				unitskillusepos .@id[3],91,5,.@x+4,.@y-4,-5;
		sleep 10;
			if(unitexists(.@id[0]) == true)
				unitskillusepos .@id[0],91,5,.@x+9,.@y,-5;
			if(unitexists(.@id[1]) == true)              
				unitskillusepos .@id[1],91,5,.@x-9,.@y,-5;
			if(unitexists(.@id[2]) == true)              
				unitskillusepos .@id[2],91,5,.@x,.@y+9,-5;
			if(unitexists(.@id[3]) == true)              
				unitskillusepos .@id[3],91,5,.@x,.@y-9,-5;
			if(unitexists(.@id[0]) == true)
				unitskillusepos .@id[0],91,5,.@x+7,.@y+7,-5;
			if(unitexists(.@id[1]) == true)                
				unitskillusepos .@id[1],91,5,.@x-7,.@y-7,-5;
			if(unitexists(.@id[2]) == true)                
				unitskillusepos .@id[2],91,5,.@x-7,.@y+7,-5;
			if(unitexists(.@id[3]) == true)                
				unitskillusepos .@id[3],91,5,.@x+7,.@y-7,-5;
		sleep 10;
			if(unitexists(.@id[0]) == true)
				unitskillusepos .@id[0],91,5,.@x+4,.@y+6,-5;
			if(unitexists(.@id[1]) == true)                
				unitskillusepos .@id[1],91,5,.@x-4,.@y+6,-5;
			if(unitexists(.@id[2]) == true)                
				unitskillusepos .@id[2],91,5,.@x+4,.@y-6,-5;
			if(unitexists(.@id[3]) == true)                
				unitskillusepos .@id[3],91,5,.@x-4,.@y-6,-5;
		sleep 1000;
		break;
	case 4: //交叉火牆
		unittalk 'BossID, "!！";
		sleep 1000;
		monster .@map$,.@x+1,.@y+1,"--ja--",3095,1,.@label$;
		.@id[0] = $@mobid[0];
		monster .@map$,.@x-1,.@y-1,"--ja--",3095,1,.@label$;
		.@id[1] = $@mobid[0];
		monster .@map$,.@x-1,.@y+1,"--ja--",3095,1,.@label$;
		.@id[2] = $@mobid[0];
		monster .@map$,.@x+1,.@y-1,"--ja--",3095,1,.@label$;
		.@id[3] = $@mobid[0];
		for(.@i = 0; .@i < 4; .@i++) {
			setunitdata .@id[.@i], 29, 1;
			unitskilluseid .@id[.@i], 353, 1;
		}
		sleep 500;
		for(.@i = 0; .@i <= 8; .@i++){
			if(unitexists(.@id[0]) == true)
				unitskillusepos .@id[0],18,5,.@x+.@i,.@y+.@i,-5;
			if(unitexists(.@id[1]) == true)                    
				unitskillusepos .@id[1],18,5,.@x+.@i,.@y-.@i,-5;
			if(unitexists(.@id[2]) == true)                    
				unitskillusepos .@id[2],18,5,.@x-.@i,.@y-.@i,-5;
			if(unitexists(.@id[3]) == true)                    
				unitskillusepos .@id[3],18,5,.@x-.@i,.@y+.@i,-5;
			sleep 10;
		}
		break;
	case 5: //隨機傳送
		if(rand(100) > 60){
			donpcevent instance_npcname("#CGH_BOSS")+"::Onrandwarp";//呼叫隨機傳送事件
		}
		break;
	
	case 6: //闇審判事件
		unittalk 'BossID, "！！";
		donpcevent instance_npcname("#Dc_GRANDCROSS")+"::OnEnable";//呼叫審判事件
		set 'bossf2 , 1;
		break;
	}
	sleep 1000;
	killmonster .@map$,.@label$;
	if(!'bossf2)
		initnpctimer;
	end;
	}
}

1@gl_he,2,2,4	script	#bossarea	-1,{

OnEnable:
	initnpctimer;
	enablenpc instance_npcname("#bossarea");
	set .outside,0;
	set 'fail,0;
	sleep 2000;
	end;

Onstoptime:
	stopnpctimer;
	end;

OnTimer10000://10秒判定一次 離開超過30秒重新打
	getunitdata 'BossID,'amdaraisdata;
	.@x = 'amdaraisdata[6];
	.@y = 'amdaraisdata[7];
	if (.@x < 150 || .@x > 165 || .@y > 245 || .@y <232){
		set .outside,.outside + 1;
		set .@map$,instance_mapname("1@gl_he");
		mapannounce .@map$, "奧斯卡：間答萊屍的幻影已經離開中間一定時間了，要是讓她離開太久的話這個時空的臨界點將會被突破，我就無法順利完成淨化了.",bc_map,"0xA42D00";
	}
	else{
		set .outside,0;
	}
	initnpctimer;
	if (.outside >= 3){
		set 'fail,1;
		donpcevent instance_npcname("#CGH_BOSS")+"::Onstoptime";
		set 'bossf2 , 1;
		donpcevent instance_npcname("#CGH_BOSS")+"::Onstoptime";
		donpcevent instance_npcname("#bossarea")+"::Onstoptime";
		hideoffnpc instance_npcname("奧斯卡#oscar_4");
		unitkill 'BossID;
		stopnpctimer;
		detachnpctimer;
		set .@map$,instance_mapname("1@gl_he");
		mapannounce .@map$, "間答萊屍的幻影：看來你沒有資格當我的新主人",bc_map,"0xFF3333";
		
	}
	end;

}




1@gl_he,172,206,4	script	#Dc_GRANDCROSS	-1,{

OnEnable:
	enablenpc instance_npcname("#Dc_GRANDCROSS");
	donpcevent instance_npcname("#Dc_GRANDCROSS")+"::OnStart";
	end;
OnStart:
	set .@map$,instance_mapname("1@gl_he");
	set .@labelG$, instance_npcname("#Dc_GRANDCROSS")+"::OnKillGRANDCROSS";
	setarray 'gid[0], 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0;
	set .@k,0;
	for(.@i = 224; .@i <= 251; .@i=.@i+9){
		set  'gid[.@k], bg_monster(0,.@map$,144,.@i,"受汙染的骨頭荊棘",20581,.@labelG$);
		set  'gid[.@k+1], bg_monster(0,.@map$,153,.@i,"受汙染的骨頭荊棘",20581,.@labelG$);
		set  'gid[.@k+2], bg_monster(0,.@map$,162,.@i,"受汙染的骨頭荊棘",20581,.@labelG$);
		set  'gid[.@k+3], bg_monster(0,.@map$,171,.@i,"受汙染的骨頭荊棘",20581,.@labelG$);
		set .@k,.@k+4;
	}

	initnpctimer;
	end;

OnKillGRANDCROSS:
	end;

OnGRANDCROSS:
	for(.@i = 0; .@i <= 15; .@i++){
	
		if(unitexists('gid[.@i]) == true){
			setunitdata 'gid[.@i], 2, 50;
			getunitdata 'gid[.@i],'mobGRANDCROSS;
			.@x = 'mobGRANDCROSS[6];
			.@y = 'mobGRANDCROSS[7];
			unitskillusepos 'gid[.@i],339,5,.@x,.@y,1;
		}
	}
	end;

OnTimer15000:
	donpcevent instance_npcname("#Dc_GRANDCROSS")+"::OnGRANDCROSS";
	end;
OnTimer17000:
	donpcevent instance_npcname("#Dc_GRANDCROSS")+"::OnGRANDCROSS";
	end;
OnTimer18000:
	donpcevent instance_npcname("#Dc_GRANDCROSS")+"::OnGRANDCROSS";
	end;
OnTimer20000:
	donpcevent instance_npcname("#Dc_GRANDCROSS")+"::OnGRANDCROSS";
	end;
OnTimer22000:
	donpcevent instance_npcname("#Dc_GRANDCROSS")+"::OnGRANDCROSS";
	end;
OnTimer24000:
	donpcevent instance_npcname("#Dc_GRANDCROSS")+"::OnGRANDCROSS";
	end;
OnTimer26500:
	for(.@i = 0; .@i <= 15; .@i++){
		if(unitexists('gid[.@i]) == true){
			unitkill 'gid[.@i];
		}
	
	}
	donpcevent instance_npcname("#CGH_BOSS")+"::Onstarttime";
	set 'bossf2 , 0;
	end;
	
}


//===================================
//--------------END--------------------

//物品掉落設定
1@gl_he,158,240,3	script	寶箱#chatnmtop	10005,{
	setarray .@gain[0],25864,25865,25866,25867;//獎勵物品設定
	setarray .@item25864min[1],3,3,3,4,5,6,6,6,8,8;
	setarray .@item25864max[1],5,6,7,8,9,10,10,10,10,10;

	setarray .@item25865min[1],0,0,1,1,1,1,2,2,2,2;
	setarray .@item25865max[1],0,0,3,4,4,5,5,6,6,7;

	setarray .@item25866min[1],0,0,0,0,1,3,5,8,11,14;
	setarray .@item25866max[1],0,0,0,0,3,7,11,16,22,28;

	setarray .@item25867min[1],6,7,10,12,15,17,18,19,20,22;
	setarray .@item25867max[1],15,20,25,30,30,30,30,30,30,30;
	getmapxy(.@map$,.@x,.@y,BL_NPC,instance_npcname(strnpcinfo(0)));
	specialeffect 10;
	hideonnpc instance_npcname(strnpcinfo(0));
	
	for(.@i = 0; .@i <= rand(.@item25864min['selectdiff-1],.@item25864max['selectdiff-1]); .@i++ ){
		makeitem .@gain[0],1,.@map$,.@x+rand(5)-1,.@y+rand(5)-1;
	}
	for(.@i = 0; .@i <= rand(.@item25867min['selectdiff-1],.@item25867max['selectdiff-1]); .@i++ ){
		makeitem .@gain[3],1,.@map$,.@x+rand(5)-1,.@y+rand(5)-1;
	}
	if ('selectdiff >= 3){
		for(.@i = 0; .@i <= rand(.@item25865min['selectdiff-1],.@item25865max['selectdiff-1]); .@i++ ){
			makeitem .@gain[1],1,.@map$,.@x+rand(5)-1,.@y+rand(5)-1;
		}
	}
	if ('selectdiff >= 5){
		for(.@i = 0; .@i <= rand(.@item25866min['selectdiff-1],.@item25866max['selectdiff-1]); .@i++ ){
			makeitem .@gain[2],1,.@map$,.@x+rand(5)-1,.@y+rand(5)-1;
		}
	}
	end;
}

1@gl_he,158,247,1	script	奧斯卡#oscar_5	4_ED_OSCAR,{

	//危險的挑戰任務獎勵
	if(checkquest(12495,HUNTING)==2 ) { 
	mes "[奧斯卡]";
	mes "^0000FF封印的時間頭冠^0000001個";
	mes "這個是從闇答萊屍身上取得的戰利品。";
	next;
	getitem 25864,1*$EP1702;	//封印的時間頭冠
	erasequest 12495;
	}
	if(checkquest(12496,HUNTING)==2 ) { 
	mes "[奧斯卡]";
	mes "^0000FF封印的時間頭冠^0000001個";
	mes "這個是從希梅爾茲身上取得的戰利品。";
	next;
	getitem 25864,1*$EP1702;	//封印的時間頭冠
	erasequest 12496;
	}

	if (is_party_leader())
	{
		mes "[奧斯卡]";
		mes "恭喜你完成副本";
		mes "這是我提供的獎勵你就收下吧！";
		next;
		addrid(2,1,'party_id);
		//---設定難度(目前為一階段打1次才會升一階)
		set #CGHTYPENUM,#CGHTYPENUM+1;
		if (#CGHTYPENUM >= 0)
		{
			set #CGHTYPE,#CGHTYPE+1;
			set #CGHTYPENUM,0;
		}
		if (#CGHTYPE >= 10)
		{
			set #CGHTYPE,10;
			set #CGHTYPENUM,0;
		}
		//next;
		//mes "在這裡崩塌之前";
		//mes "我會用盡我的力氣替你傳送出去。";
		//mes "再會了。勇者。";
		//next;
		'ClearInatance = 1;
		getmapxy(.@m$,'passx,'passy,BL_PC);
		donpcevent instance_npcname("副本寶箱#1@gl_he")+"::OnEnable";
		disablenpc instance_npcname("奧斯卡#oscar_5");
		end;
	}
	else
	{
		showscript "請隊長與我領取獎勵。",getcharid(3),SELF;
		end;
	}
end;
}


1@gl_he,0,0,0	script	副本寶箱#1@gl_he	obj_a2,{
	if ( !'ClearInatance ){
		message strcharinfo(0),"遊戲提示 : 副本尚未完成!";
		end;
	}

	//副本任務獎勵1
	if(checkquest(32022,HUNTING)==2 ) {
	dispbottom "克雷斯特漢姆挑戰模式任務完成，以下是你的獎勵!!"; 
	set #Instance_Points,#Instance_Points+20;	//副本積分+20
	dispbottom "副本積分+20";
	dispbottom "目前副本積分 ^0000ff"+#Instance_Points+"^000000 點";
	getitem 61006,10;	//商城1點金幣
	erasequest 32022; }
	//副本任務獎勵2
	if(checkquest(32026,HUNTING)==2 ) {
	dispbottom "克雷斯特漢姆挑戰模式任務完成，以下是你的獎勵!!"; 
	set #Instance_Points,#Instance_Points+20;	//副本積分+20
	dispbottom "副本積分+20";
	dispbottom "目前副本積分 ^0000ff"+#Instance_Points+"^000000 點";
	getitem 61006,10;	//商城1點金幣
	erasequest 32026; }

	//危險的挑戰任務獎勵
	if(checkquest(12495,HUNTING)==2 ) { 
	mes "[奧斯卡]";
	mes "^0000FF封印的時間頭冠^0000001個";
	mes "這個是從闇答萊屍身上取得的戰利品。";
	next;
	getitem 25864,1*$EP1702;	//封印的時間頭冠
	erasequest 12495;
	}
	if(checkquest(12496,HUNTING)==2 ) { 
	mes "[奧斯卡]";
	mes "^0000FF封印的時間頭冠^0000001個";
	mes "這個是從希梅爾茲身上取得的戰利品。";
	next;
	getitem 25864,1*$EP1702;	//封印的時間頭冠
	erasequest 12496;
	}
	callfunc "offical_instance_finish",46;
	warp $Instance_MA$,$Instance_MAP_X,$Instance_MAP_Y;
	end;

OnEnable:
	enablenpc instance_npcname(strnpcinfo(0));
	movenpc instance_npcname(strnpcinfo(0)),'passx,'passy;
	end;
OnDisable:
	disablenpc instance_npcname(strnpcinfo(0));
	end;
OnInstanceInit:
	disablenpc instance_npcname(strnpcinfo(0));
	'map_reward$ = instance_mapname("1@gl_he");
	end;
}