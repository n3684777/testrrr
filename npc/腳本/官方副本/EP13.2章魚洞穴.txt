
vdistrict1,15,76,4	script	章魚洞穴管理者	551,{

OnStart:
	//副本任務獎勵
	if(checkquest(32012,HUNTING)==2 ) { 
	dispbottom "章魚洞穴任務完成，以下是你的獎勵!!"; 
	set #Instance_Points,#Instance_Points+10;	//副本積分+10
	dispbottom "副本積分+10";
	dispbottom "目前副本積分 ^0000ff"+#Instance_Points+"^000000 點";
	erasequest 32012; }

	//挑戰次數
	if ( day_Instance_Bout_03 < gettime(8) ){
	set Instance_Bout_03,10;
	set day_Instance_Bout_03,gettime(8);	//一年中的一天
	end; }
	if (Instance_Bout_03 < 1) {
		mes "挑戰次數不足";
		close;
	}

	set .@party_id,getcharid(1);
	set .@p_name$,getpartyname(.@party_id);

	if (!.@party_id) {
		mes "[章魚洞穴管理者]";
		mes "你的隊伍成員呢?";
		close;
	}
	
	.@md_name$ = "章魚洞穴";
	
	if (getcharid(0) == getpartyleader(.@party_id,2))
		set @menu$, "創建"+.@md_name$+":進入"+.@md_name$;
	else
		set @menu$, ":進入"+.@md_name$;
		
	mes "[章魚洞穴管理者]";
	mes "副本獎勵倍數[ 無 ]";
	mes "挑戰剩餘次數[ ^FF0000"+Instance_Bout_03+"^000000 ]";
	mes "副本獎勵：";
	mes "官方副本積分x10";
	mes "你想要幹嘛呢？";
	next;
	.@i = select(@menu$)-1;
	switch(.@i) {
	case 0:
		if(BaseLevel < 100) {
			mes "[章魚洞穴管理者]";
			mes "裡面是很危險的…"; 
			mes "還是等到 Lv.100 後再來吧…"; 
			close;
		}
		
		if (instance_create(.@md_name$) < 0) {
			mes "[章魚洞穴管理者]";
			mes "隊伍名稱: "+.@p_name$;
			mes "隊伍隊長: "+strcharinfo(0);
			mes "^0000ff"+.@md_name$+" ^000000- 創建失敗!";
			close;
		}

		mapannounce strnpcinfo(4),"隊伍 "+getpartyname(.@party_id)+" 準備挑戰【"+.@md_name$+"】副本！",bc_map,"0x33ea91";	
		mes "[章魚洞穴管理者]";
		mes ""+.@md_name$+"已經創立了∼";
		mes "請選擇^0066CC進入"+.@md_name$+"^000000。";
		
		.@iid = instance_id(IM_PARTY);
		set getinstancevar('party_id, .@iid), getcharid(1);
		//入場記錄時間
		set Instance_Annal,gettimetick(2);
		close;
	case 1:
		if(BaseLevel < 140) {
			mes "[章魚洞穴管理者]";
			mes "裡面是很危險的…"; 
			mes "還是等到 Lv.140 後再來吧…"; 
			close;
		}
	//入場結束
	if ( getinstancevar('Win,instance_id(IM_PARTY)) ) {
	mes "副本已經開始了，無法再次進入。";
	close;
	}
		switch(instance_enter(.@md_name$)) {
		case IE_OTHER:
			mes "不明錯誤。";
			close;
		case IE_NOINSTANCE:
			mes .@md_name$+" 副本不存在。";
			mes "隊長尚未申請記憶迷宮。";
			close;
		case IE_NOMEMBER:
			mes "只有申請 "+.@md_name$+" 副本的隊員才可以進入。";
			close;
		case IE_OK:
			//mapannounce strnpcinfo(4),"隊伍 " + getpartyname(.@party_id) + " 裡的勇者 " + strcharinfo(0) + " 開始進行【"+.@md_name$+"】副本了",bc_map,"0x00ff99";			
			//扣除挑戰次數
			set Instance_Bout_03,Instance_Bout_03-1;

			//副本任務
			if(checkquest(32012,HUNTING)<0 ){setquest 32012;}
			//禁止多開偵測
			if(getsameipinfo(getcharip(), strcharinfo(3)) > $Windows){
			//允許多開名單
			for(set .@a,0; .@a < getarraysize($@PartyWindowsows$); set .@a,.@a+1){
			if(getcharip()==$@PartyWindowsows$[.@a]){
			end; }}
			mes "道館跟副本禁止多開!";
			mes "檢測到副本地圖中有相同IP人物";
			mes "^777777提示：5秒後自動傳送回首都^000000";
			sleep2 5000;
			warp "prontera",102,118;
			end; }
			close;
		}
	}
	close;
	
OnInit:
	waitingroom "章魚洞穴",0;
	end;
}



// Instance Scripts
//============================================================
1@cash,199,99,0	script	oct_enter	139,4,4,{
	end;
OnTouch:
	donpcevent instance_npcname("oct_enter_broad")+"::OnEnable";
	specialeffect EF_BASH;
	disablenpc instance_npcname("oct_enter");
	end;
}

1@cash,1,1,0	script	oct_enter_broad	-1,{
	end;
OnInstanceInit:
	donpcevent instance_npcname("oct_enter_broad")+"::OnDisable";
	end;
OnEnable:
	enablenpc instance_npcname("oct_enter_broad");
	donpcevent instance_npcname("oct_foot_4")+"::OnEnable";
	donpcevent instance_npcname("oct_mob_con")+"::OnEnable";
	initnpctimer;
	end;
OnDisable:
	disablenpc instance_npcname("oct_enter_broad");
	end;
OnTimer1000:
	mapannounce instance_mapname("1@cash"),"變態章魚: 大膽的蠢貨! 你竟敢.. 竟敢.. 闖入我的地盤!!",bc_map,"0xFFFF00";
	end;
OnTimer4000:
	mapannounce instance_mapname("1@cash"),"變態章魚: 哼哼! 我已經擋住了所有的洞口! 你們已經無計可施了!",bc_map,"0xFFFF00";
	end;
OnTimer7000:
	mapannounce instance_mapname("1@cash"),"變態章魚: 我的孩子們~ 你們看看是誰來了! 沒錯! 是新玩具噢! 盡情的玩弄吧!",bc_map,"0xFFFF00";
	stopnpctimer;
	donpcevent instance_npcname("oct_enter_broad")+"::OnDisable";
	end;
}

1@cash,3,3,0	script	oct_foot_4	-1,{
	end;
OnInstanceInit:
	donpcevent instance_npcname("oct_foot_4")+"::OnDisable";
	//副本開始關閉入場
	sleep 60000;
	'win = 1;
	instance_announce instance_id(1),"副本開始!!",bc_all,0xD7BA98;
	end;
OnEnable:
	enablenpc instance_npcname("oct_foot_4");
	donpcevent instance_npcname("oct_foot1")+"::OnEnable";
	donpcevent instance_npcname("oct_foot2")+"::OnEnable";
	donpcevent instance_npcname("oct_foot3")+"::OnEnable";
	donpcevent instance_npcname("oct_foot4")+"::OnEnable";
	set .@map$, instance_mapname("1@cash");
	monster .@map$,20,114,"深海章魚腳",2193,1,instance_npcname("oct_foot_4")+"::OnMyMobDead";
	monster .@map$,88,190,"深海章魚腳",2193,1,instance_npcname("oct_foot_4")+"::OnMyMobDead";
	monster .@map$,307,215,"深海章魚腳",2193,1,instance_npcname("oct_foot_4")+"::OnMyMobDead";
	monster .@map$,372,131,"深海章魚腳",2193,1,instance_npcname("oct_foot_4")+"::OnMyMobDead";
	end;
OnDisable:
	disablenpc instance_npcname("oct_foot_4");
	end;
OnMyMobDead:
	set .@map$, instance_mapname("1@cash");
	if (mobcount(.@map$,instance_npcname("oct_foot_4")+"::OnMyMobDead") < 1) {
		donpcevent instance_npcname("oct_boss_con")+"::OnEnable";
		mapannounce .@map$,"你們這些蠢貨! 我會好好的照顧照顧你們的! 來吧!",bc_map,"0x00ff99";
		enablenpc instance_npcname("oct_boss_warp");
		instance_warpall .@map$,199,99;
		end;
	}
	mapannounce .@map$,"啊啊啊! 很痛耶你知道嗎? 哼! 看來我需要別的戰略!!",bc_map,"0x00ff99";
	end;
}

1@cash,20,114,0	script	oct_foot1	139,6,6,{
	end;
OnInstanceInit:
	donpcevent instance_npcname(strnpcinfo(0))+"::OnDisable";
	end;
OnEnable:
	enablenpc instance_npcname(strnpcinfo(0));
	set .@label$, instance_npcname(strnpcinfo(0))+"::OnMyMobDead";
	set .@i, atoi(charat(strnpcinfo(0),8));
	set .@map$, instance_mapname("1@cash");
	switch(.@i) {
	case 1:
		areamonster .@map$,34,96,38,100,"章魚的小弟",2192,3,.@label$;
		areamonster .@map$,39,90,41,92,"章魚的小弟",2192,2,.@label$;
		areamonster .@map$,38,76,40,78,"章魚的小弟",2192,2,.@label$;
		areamonster .@map$,39,58,41,60,"章魚的小弟",2192,2,.@label$;
		areamonster .@map$,50,38,56,44,"章魚的小弟",2192,5,.@label$;
		areamonster .@map$,68,32,72,36,"章魚的小弟",2192,5,.@label$;
		areamonster .@map$,83,26,85,28,"章魚的小弟",2192,2,.@label$;
		areamonster .@map$,107,28,109,30,"章魚的小弟",2192,1,.@label$;
		areamonster .@map$,123,28,125,30,"章魚的小弟",2192,2,.@label$;
		areamonster .@map$,134,31,138,35,"章魚的小弟",2192,5,.@label$;
		areamonster .@map$,142,39,146,43,"章魚的小弟",2192,2,.@label$;
		break;
	case 2:
		areamonster .@map$,139,113,143,117,"章魚的小弟",2192,5,.@label$;
		areamonster .@map$,135,104,137,106,"章魚的小弟",2192,2,.@label$;
		areamonster .@map$,123,93,127,97,"章魚的小弟",2192,3,.@label$;
		areamonster .@map$,113,90,115,92,"章魚的小弟",2192,2,.@label$;
		areamonster .@map$,103,89,105,91,"章魚的小弟",2192,2,.@label$;
		areamonster .@map$,89,90,91,92,"章魚的小弟",2192,2,.@label$;
		areamonster .@map$,74,104,76,106,"章魚的小弟",2192,2,.@label$;
		areamonster .@map$,74,120,76,122,"章魚的小弟",2192,2,.@label$;
		areamonster .@map$,83,131,87,135,"章魚的小弟",2192,3,.@label$;
		areamonster .@map$,92,142,94,144,"章魚的小弟",2192,2,.@label$;
		areamonster .@map$,98,159,100,161,"章魚的小弟",2192,2,.@label$;
		areamonster .@map$,94,180,98,184,"章魚的小弟",2192,5,.@label$;
		break;
	case 3:
		areamonster .@map$,299,209,303,213,"章魚的小弟",2192,3,.@label$;
		areamonster .@map$,292,203,296,207,"章魚的小弟",2192,3,.@label$;
		areamonster .@map$,291,186,293,188,"章魚的小弟",2192,2,.@label$;
		areamonster .@map$,292,166,294,168,"章魚的小弟",2192,2,.@label$;
		areamonster .@map$,293,151,295,153,"章魚的小弟",2192,2,.@label$;
		areamonster .@map$,299,137,303,141,"章魚的小弟",2192,2,.@label$;
		areamonster .@map$,300,123,304,127,"章魚的小弟",2192,2,.@label$;
		areamonster .@map$,300,104,304,108,"章魚的小弟",2192,3,.@label$;
		areamonster .@map$,279,101,281,103,"章魚的小弟",2192,2,.@label$;
		monster .@map$,260,103,"章魚的小弟",2192,1,.@label$;
		break;
	case 4:
		areamonster .@map$,363,123,367,127,"章魚的小弟",2192,2,.@label$;
		areamonster .@map$,364,108,368,112,"章魚的小弟",2192,5,.@label$;
		areamonster .@map$,363,93,367,97,"章魚的小弟",2192,3,.@label$;
		areamonster .@map$,364,72,366,74,"章魚的小弟",2192,2,.@label$;
		areamonster .@map$,364,58,366,60,"章魚的小弟",2192,2,.@label$;
		areamonster .@map$,355,44,359,48,"章魚的小弟",2192,3,.@label$;
		areamonster .@map$,346,36,350,40,"章魚的小弟",2192,3,.@label$;
		areamonster .@map$,319,28,325,34,"章魚的小弟",2192,5,.@label$;
		areamonster .@map$,305,27,307,29,"章魚的小弟",2192,2,.@label$;
		areamonster .@map$,276,29,280,33,"章魚的小弟",2192,5,.@label$;
		areamonster .@map$,258,37,262,41,"章魚的小弟",2192,2,.@label$;
		break;
	}
	end;
OnDisable:
	killmonster instance_mapname("1@cash"),instance_npcname(strnpcinfo(0))+"::OnMyMobDead"; // Not in official script.
	disablenpc instance_npcname(strnpcinfo(0));
	end;
OnTouch:
	if (getd("."+strnpcinfo(0)+instance_id())) end;
	setd "."+strnpcinfo(0)+instance_id(),1;
	hideonnpc instance_npcname(strnpcinfo(0));
	mapannounce instance_mapname("1@cash"),"出來吧! 我的孩子們! 快來解決這些蠢貨!",bc_map,"0xFFFF00";
	initnpctimer;
	end;
OnTimer5000:
	set .@map$, instance_mapname("1@cash");
	mapannounce .@map$,"讓我們好好的教訓教訓這些蠢貨!",bc_map,"0xFFFF00";
	set .@label$, instance_npcname(strnpcinfo(0))+"::OnMyMobDead";
	set .@i, atoi(charat(strnpcinfo(0),8));
	switch(.@i) {
	case 1:
		areamonster .@map$,18,112,22,116,"章魚的小弟",2192,3,.@label$;
		areamonster .@map$,18,112,22,116,"章魚的小弟",2192,3,.@label$;
		break;
	case 2:
		areamonster .@map$,86,188,90,192,"章魚的小弟",2192,3,.@label$;
		areamonster .@map$,96,98,100,102,"章魚的小弟",2192,3,.@label$;
		break;
	case 3:
		areamonster .@map$,305,213,309,217,"章魚的小弟",2192,3,.@label$;
		areamonster .@map$,305,213,309,217,"章魚的小弟",2192,3,.@label$;
		break;
	case 4:
		areamonster .@map$,370,129,374,133,"章魚的小弟",2192,3,.@label$;
		areamonster .@map$,370,129,374,133,"章魚的小弟",2192,3,.@label$;
		break;
	}
	end;
OnTimer30000:
	setd "."+strnpcinfo(0)+instance_id(),0;
	stopnpctimer;
	set .@i, atoi(charat(strnpcinfo(0),8));
	donpcevent instance_npcname("oct_foot_exit"+.@i)+"::OnEnable";
	donpcevent instance_npcname(strnpcinfo(0))+"::OnDisable";
	end;
OnMyMobDead:
	set .@map$, instance_mapname("1@cash");
	if (mobcount(.@map$,instance_npcname(strnpcinfo(0))+"::OnMyMobDead") < 1) {
		mapannounce .@map$,"你竟然傷害了我的孩子們!!? 你會付出代價的!!!",bc_map,"0x00ff99";
		setd "."+strnpcinfo(0)+instance_id(),0;
		stopnpctimer;
		set .@i, atoi(charat(strnpcinfo(0),8));
		donpcevent instance_npcname("oct_foot_exit"+.@i)+"::OnEnable";
		donpcevent instance_npcname(strnpcinfo(0))+"::OnDisable";
		end;
	}
	end;
}
1@cash,88,190,0	duplicate(oct_foot1)	oct_foot2	139,6,6
1@cash,307,215,0	duplicate(oct_foot1)	oct_foot3	139,6,6
1@cash,372,131,0	duplicate(oct_foot1)	oct_foot4	139,6,6

1@cash,16,117,0	script	oct_foot_exit1	45,2,2,{
	end;
OnInstanceInit:
	disablenpc instance_npcname(strnpcinfo(0));
	end;
OnEnable:
	enablenpc instance_npcname(strnpcinfo(0));
	end;
OnTouch:
	warp instance_mapname("1@cash"),198,99;
	end;
}
1@cash,77,193,0	duplicate(oct_foot_exit1)	oct_foot_exit2	45,2,2
1@cash,312,214,0	duplicate(oct_foot_exit1)	oct_foot_exit3	45,2,2
1@cash,370,139,0	duplicate(oct_foot_exit1)	oct_foot_exit4	45,2,2

1@cash,15,15,0	script	oct_mob_con	-1,{
	end;
OnInstanceInit:
	donpcevent instance_npcname("oct_mob_con")+"::OnDisable";
	end;
OnEnable:
	set .@map$, instance_mapname("1@cash");
	enablenpc instance_npcname("oct_mob_con");
	monster .@map$,32,94,"海葵",1068,1;
	monster .@map$,41,101,"海葵",1068,1;
	monster .@map$,35,78,"海葵",1068,1;
	monster .@map$,35,62,"海葵",1068,1;
	monster .@map$,49,54,"海葵",1068,1;
	monster .@map$,70,28,"海葵",1068,1;
	monster .@map$,83,22,"海葵",1068,1;
	monster .@map$,99,23,"海葵",1068,1;
	monster .@map$,115,23,"海葵",1068,1;
	monster .@map$,132,25,"海葵",1068,1;
	monster .@map$,100,185,"海葵",1068,1;
	monster .@map$,92,178,"海葵",1068,1;
	monster .@map$,92,162,"海葵",1068,1;
	monster .@map$,70,121,"海葵",1068,1;
	monster .@map$,70,105,"海葵",1068,1;
	monster .@map$,105,85,"海葵",1068,1;
	monster .@map$,121,84,"海葵",1068,1;
	monster .@map$,292,209,"海葵",1068,1;
	monster .@map$,303,206,"海葵",1068,1;
	monster .@map$,290,163,"海葵",1068,1;
	monster .@map$,299,150,"海葵",1068,1;
	monster .@map$,308,141,"海葵",1068,1;
	monster .@map$,308,125,"海葵",1068,1;
	monster .@map$,295,97,"海葵",1068,1;
	monster .@map$,279,97,"海葵",1068,1;
	monster .@map$,370,111,"海葵",1068,1;
	monster .@map$,370,110,"海葵",1068,1;
	monster .@map$,371,96,"海葵",1068,1;
	monster .@map$,371,80,"海葵",1068,1;
	monster .@map$,367,55,"海葵",1068,1;
	monster .@map$,343,29,"海葵",1068,1;
	monster .@map$,327,24,"海葵",1068,1;
	monster .@map$,311,22,"海葵",1068,1;
	monster .@map$,295,22,"海葵",1068,1;
	monster .@map$,279,22,"海葵",1068,1;
	areamonster .@map$,30,67,50,87,"Stapo",1784,1;
	areamonster .@map$,102,19,122,39,"Stapo",1784,1;
	areamonster .@map$,89,138,109,158,"Stapo",1784,1;
	areamonster .@map$,112,83,132,103,"Stapo",1784,1;
	areamonster .@map$,283,168,303,188,"Stapo",1784,1;
	areamonster .@map$,292,97,312,117,"Stapo",1784,1;
	areamonster .@map$,355,64,375,84,"Stapo",1784,1;
	areamonster .@map$,317,17,337,37,"Stapo",1784,1;
	donpcevent instance_npcname("oct_backattack1")+"::OnEnable";
	donpcevent instance_npcname("oct_backattack2")+"::OnEnable";
	donpcevent instance_npcname("oct_backattack3")+"::OnEnable";
	donpcevent instance_npcname("oct_backattack4")+"::OnEnable";
	donpcevent instance_npcname("oct_mob_con")+"::OnDisable";
	end;
OnDisable:
	disablenpc instance_npcname("oct_mob_con");
	end;
}

1@cash,45,53,0	script	oct_backattack1	139,3,3,{
	end;
OnInstanceInit:
	disablenpc instance_npcname("oct_backattack1");
	end;
OnTouch:
	set .@map$, instance_mapname("1@cash");
	monster .@map$,39,59,"海葵",1068,1;
	monster .@map$,40,59,"海葵",1068,1;
	monster .@map$,41,59,"海葵",1068,1;
	monster .@map$,47,50,"海葵",1068,1;
	monster .@map$,48,50,"海葵",1068,1;
	monster .@map$,49,50,"海葵",1068,1;
	monster .@map$,41,53,"Octopus's Henchman",2192,1;
	mapannounce .@map$,"不要讓他們過來, 快阻止他們!!!",bc_map,"0x00ff99";
	specialeffect EF_BASH;
	disablenpc instance_npcname("oct_backattack1");
	end;
OnEnable:
	enablenpc instance_npcname("oct_backattack1");
	end;
}

1@cash,78,99,0	script	oct_backattack2	139,3,3,{
	end;
OnInstanceInit:
	disablenpc instance_npcname("oct_backattack2");
	end;
OnTouch:
	if (getd("."+instance_id())) end;
	setd "."+instance_id(),1;
	initnpctimer;
	set .@map$, instance_mapname("1@cash");
	monster .@map$,71,105,"章魚的小弟",2192,1;
	mapannounce .@map$,"章魚的巢穴快要被清空了, 加油!!!",bc_map,"0x00ff99";
	hideonnpc instance_npcname("oct_backattack2");
	end;
OnTimer2000:
	set .@map$, instance_mapname("1@cash");
	monster .@map$,71,105,"章魚的小弟",2192,1;
	mapannounce .@map$,"快跑啊!! 加油!!",bc_map,"0x00ff99";
	end;
OnTimer4000:
	set .@map$, instance_mapname("1@cash");
	monster .@map$,71,105,"章魚的小弟",2192,1;
	mapannounce .@map$,"沒有時間了, 快點出來!!",bc_map,"0x00ff99";
	end;
OnTimer6000:
	set .@map$, instance_mapname("1@cash");
	monster .@map$,71,105,"章魚的小弟",2192,3;
	mapannounce .@map$,"滴答.. 滴答.. 快點好嗎?",bc_map,"0x00ff99";
	end;
OnTimer8000:
	set .@map$, instance_mapname("1@cash");
	monster .@map$,71,105,"章魚的小弟",2192,1;
	mapannounce .@map$,"不要磨磨蹭蹭的! 我們要沒有時間了!!!",bc_map,"0x00ff99";
	stopnpctimer;
	end;
OnEnable:
	enablenpc instance_npcname("oct_backattack2");
	end;
}

1@cash,299,144,0	script	oct_backattack3	139,3,3,{
	end;
OnInstanceInit:
	disablenpc instance_npcname("oct_backattack3");
	end;
OnTouch:
	if (getd("."+instance_id())) end;
	setd "."+instance_id(),1;
	set .@map$, instance_mapname("1@cash");
	monster .@map$,293,153,"章魚的小弟",2192,1;
	monster .@map$,294,152,"章魚的小弟",2192,1;
	monster .@map$,292,153,"章魚的小弟",2192,1;
	monster .@map$,293,151,"章魚的小弟",2192,1;
	monster .@map$,293,152,"深海魷魚",2175,1;
	mapannounce .@map$,"哇哈哈哈哈哈哈! 驚訝吧??!!",bc_map,"0x00ff99";
	initnpctimer;
	hideonnpc instance_npcname("oct_backattack3");
	end;
OnTimer5000:
	mapannounce instance_mapname("1@cash"),"...看來我們之間有間諜.",bc_map,"0x00ff99";
	stopnpctimer;
	end;
OnEnable:
	enablenpc instance_npcname("oct_backattack3");
	end;
}

1@cash,336,36,0	script	oct_backattack4	139,3,3,{
	end;
OnInstanceInit:
	disablenpc instance_npcname("oct_backattack4");
	end;
OnTouch:
	if (getd("."+instance_id())) end;
	setd "."+instance_id(),1;
	set .@map$, instance_mapname("1@cash");
	monster .@map$,332,37,"章魚的小弟",2192,1;
	monster .@map$,332,36,"章魚的小弟",2192,1;
	monster .@map$,332,35,"章魚的小弟",2192,1;
	monster .@map$,332,34,"章魚的小弟",2192,1;
	monster .@map$,341,37,"章魚的小弟",2192,1;
	monster .@map$,341,36,"章魚的小弟",2192,1;
	monster .@map$,341,35,"章魚的小弟",2192,1;
	monster .@map$,341,34,"章魚的小弟",2192,1;
	monster .@map$,260,40,"深海魷魚",2175,1;
	monster .@map$,260,41,"深海魷魚",2175,1;
	monster .@map$,260,39,"深海魷魚",2175,1;
	monster .@map$,259,40,"深海魷魚",2175,1;
	monster .@map$,261,40,"深海魷魚",2175,1;
	mapannounce .@map$,"哼哼! 我的夾擊戰術果然不錯! 敵人雖然很強大但我們也不會輸的! 呃? 我的傭兵呢?",bc_map,"0x00ff99";
	initnpctimer;
	hideonnpc instance_npcname("oct_backattack4");
	end;
OnTimer5000:
	mapannounce instance_mapname("1@cash"),"魷魚傭兵: 呃...嗯...走錯邊了啦.. 這個方向沒有敵人啊!",bc_map,"0x00ff99";
	end;
OnTimer7000:
	mapannounce instance_mapname("1@cash"),"變態章魚: 你..你們這些智障! 難道不會看地圖嗎??!! 沒用的東西!!",bc_map,"0x00ff99";
	stopnpctimer;
	end;
OnEnable:
	enablenpc instance_npcname("oct_backattack4");
	end;
}

1@cash,2,2,0	script	oct_boss_con	-1,{
	end;
OnInstanceInit:
	donpcevent instance_npcname("oct_boss_con")+"::OnDisable";
	end;
OnDisable:
	disablenpc instance_npcname("oct_boss_con");
	end;
OnEnable:
	enablenpc instance_npcname("oct_boss_con");
	donpcevent instance_npcname("oct_boss_foot")+"::OnEnable";
	monster instance_mapname("1@cash"),199,188,"邪惡章魚",2194,1,instance_npcname("oct_boss_con")+"::OnMyMobDead";
	initnpctimer;
	end;
OnTimer7000:
	callsub OnAnnounce,
		"你難道要攻擊我嗎? 我只有一個人!",
		"不要啊! 我上禮拜才被墨水公司開除掉啊!!",
		"噁心的章魚: 啊啊啊啊! 來吧!!";
	end;
OnTimer14000:
	callsub OnAnnounce,
		"為.. 為什麼你就是不懂我的心呢?!",
		"你知道我有多努力的想在這裡生存嗎? 你們是無法擊敗我的!",
		"你.. 我會摧毀你們所有人...!";
	end;
OnTimer21000:
	callsub OnAnnounce,
		"什麼? 你竟然說我是噁心的章魚!? 你這可悲的傢伙!",
		"我..我們可以不要打了嗎? 一起尋找別的解決方案吧? 如..如何?",
		"就算你今天打敗我了!! 我還是會在回來的!!!";
	end;
OnTimer28000:
	callsub OnAnnounce,
		"身為雄性.. 我的本能就是求偶交配啊! 我到底做錯了什麼!!!",
		"我們人類之間真的有必要這樣暴力相向嗎?! 呃不對, 我是隻章魚QAQ...",
		"夠了!! 我受夠了!!";
	end;
OnTimer35000:
	callsub OnAnnounce,
		"難道我今年的聖誕節又要一個人過了嗎...",
		"你知道年輕人的失業率最近頗高的... 你在這裡跟我耗對你不會有任何好處的!!",
		"看來我只能到這裡了...";
	end;
OnTimer42000:
	callsub OnAnnounce,
		"摸摸你自己的良心.. 你這樣做對嗎???",
		"我們不要在這裡拳腳相向了! 我們照著法律走!",
		"是嗎? 是嗎?! 你想跟我戰鬥到最後嗎?!?!";
	end;
OnTimer49000:
	callsub OnAnnounce,
		"阿阿阿阿阿阿阿阿... 咳! 咳!",
		"噁心的章魚: 暴力在任何地方都是不合法的! 不!合!法!",
		"這裡是我的地盤啊啊啊啊啊啊!";
	stopnpctimer;
	initnpctimer;
	end;
OnAnnounce:
	mapannounce instance_mapname("1@cash"),"Disgusting Octopus : "+getarg(rand(3)),bc_map,"0xFFFF00";
	return;
OnMyMobDead:
	set .@map$, instance_mapname("1@cash");
	if (mobcount(.@map$,instance_npcname("oct_boss_con")+"::OnMyMobDead") < 1) {
		mapannounce .@map$,"噁心的章魚: 哼! 這次算你贏了可以了吧? 下一次我就會讓你知道我真正厲害!!",bc_map,"0xFFFF00";
		enablenpc instance_npcname("oct_exit_1");
		enablenpc instance_npcname("oct_exit_2");
		donpcevent instance_npcname("oct_boss_foot")+"::OnDisable";
		stopnpctimer;
		donpcevent instance_npcname("oct_boss_con")+"::OnDisable";
		end;
	}
	end;
}

1@cash,4,4,0	script	oct_boss_foot	-1,{
	end;
OnInstanceInit:
	donpcevent instance_npcname("oct_boss_foot")+"::OnDisable";
	end;
OnEnable:
	enablenpc instance_npcname("oct_boss_foot");
	initnpctimer;
	end;
OnCall:
	set .@map$, instance_mapname("1@cash");
	if (mobcount(.@map$,instance_npcname("oct_boss_foot")+"::OnMyMobDead") < 100) {
		switch(rand(2)) {
		case 0:
			mapannounce .@map$,"噁心的章魚: 你知道我有多少腿嗎? 沒關係, 因為我有無限的腿!!",bc_map,"0xFFFF00";
			areamonster .@map$,192,181,206,195,"深海章魚腳",2193,1,instance_npcname("oct_boss_foot")+"::OnMyMobDead";
			break;
		case 1:
			mapannounce .@map$,"噁心的章魚: 我無法撐下去了, 我的孩子們, 快出來吧!!",bc_map,"0xFFFF00";
			areamonster .@map$,192,181,206,195,"章魚的小弟",2192,5,instance_npcname("oct_boss_foot")+"::OnMyMobDead";
			break;
		}
	}
	initnpctimer;
	end;
OnTimer10000:
	stopnpctimer;
	donpcevent instance_npcname("oct_boss_foot")+"::OnCall";
	end;
OnDisable:
	stopnpctimer;
	killmonster instance_mapname("1@cash"),instance_npcname("oct_boss_foot")+"::OnMyMobDead"; // Not in official script.
	disablenpc instance_npcname("oct_boss_foot");
	end;
OnMyMobDead:
	end;
}

1@cash,198,116,0	script	oct_boss_warp	45,2,2,{
	end;
OnInstanceInit:
	disablenpc instance_npcname("oct_boss_warp");
	end;
OnTouch:
	warp instance_mapname("1@cash"),210,172;
	end;
}

1@cash,190,208,0	script	oct_exit_1	45,2,2,{
	end;
OnInstanceInit:
	disablenpc instance_npcname(strnpcinfo(0));
	end;
OnTouch:
	mes "你想離開 章魚洞穴 嗎?";
	next;
	if(select("不要!:拜託快讓我離開這!") == 2)
	//副本任務獎勵
	if(checkquest(32012,HUNTING)==2 ) { 
	dispbottom "章魚洞穴任務完成，以下是你的獎勵!!"; 
	callfunc "offical_instance_finish",15;
	set #Instance_Points,#Instance_Points+10;	//副本積分+10
	dispbottom "副本積分+10";
	dispbottom "目前副本積分 ^0000ff"+#Instance_Points+"^000000 點";
	erasequest 32012; }
	warp $Instance_MA$,$Instance_MAP_X,$Instance_MAP_Y;
	close;
}
1@cash,198,82,0	duplicate(oct_exit_1)	oct_exit_2	45,2,2
// This is never enabled in the official script.
//1@cash,200,161,0	duplicate(oct_exit_1)	oct_exit_3	45,2,2

// Warp Portals
//============================================================
1@cash,193,82,0	warp	oct_door1	2,2,1@cash,151,48
1@cash,154,53,0	warp	oct_door2	2,2,1@cash,197,87
1@cash,187,99,0	warp	oct_door3	2,2,1@cash,148,128
1@cash,153,133,0	warp	oct_door4	2,2,1@cash,197,99
1@cash,209,100,0	warp	oct_door5	2,2,1@cash,250,111
1@cash,244,118,0	warp	oct_door6	2,2,1@cash,199,100
1@cash,209,83,0	warp	oct_door7	2,2,1@cash,252,46
1@cash,246,53,0	warp	oct_door8	2,2,1@cash,203,83
